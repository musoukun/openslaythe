## Read only data for a type of consumable.
extends SerializableData
class_name ConsumableData

@export var consumable_name: String = ""
## How this appears in tooltips.
@export var consumable_description: String = ""
## How this consumable is described as being used. Eg Use, Throw, Drink.
@export var consumable_use_text: String = "Drink"
## Display texture path for the consumable.
@export var consumable_texture_path: String = ""

## The color of the consumable. Affects appearances in consumable packs.
@export var consumable_color_id: String = ""

## If true, this consumable cannot be used by the player via the UI (use button will be disabled).
## This is useful for consumables that have automatic conditions for use.
## NOTE: It is also possibly to dynamically disable consumable use with this
## flag via get_consumable_intercepted_action_results().
@export var consumable_use_disabled: bool = false

## Consumables can be made to cost energy. If 0 will not display as costing anything.
@export var consumable_energy_cost: int = 0

## The actions to take if this consumable is in your inventory at the start of combat.
## This can allow for consumables with passive effects while in inventory.
@export var consumable_initial_combat_actions: Array[Dictionary] = []

## If the consumable requires clicking on an enemy
@export var consumable_requires_target: bool = false

enum CONSUMABLE_RARITIES {COMMON, UNCOMMON, RARE, LEGENDARY}
@export var consumable_rarity: int = CONSUMABLE_RARITIES.COMMON

## Values that are duplicated into a CardPlayRequest for the actions when the consumable is used.
## NOTE: Reminder that ConsumableData is read only. Do not mutate these values.
@export var consumable_values: Dictionary[String, Variant] = {}

## Actions performed when the consumable is used
@export var consumable_actions: Array[Dictionary] = []

## Generates an ActionConsumable, intercepts it, then returns the intercepted results in dictionary form.
## This cuts down on a lot of messy code used in getting intercepted values used for using a consumable.
func get_consumable_intercepted_action_results() -> Dictionary[String, Variant]:
	var intercepted_action_results: Dictionary[String, Variant] = {}
	
	# will only intercept during a run for performance reasons
	if Global.is_run:
		var card_play_request: CardPlayRequest = HandManager.create_card_play_request(null, null, false, false) # generate fake request
		card_play_request.card_values = consumable_values.duplicate()
		card_play_request.card_values["consumable_id"] = object_id
		card_play_request.card_values["consumable_use_text"] = consumable_use_text
		card_play_request.card_values["consumable_use_disabled"] = consumable_use_disabled
		card_play_request.card_values["consumable_energy_cost"] = consumable_energy_cost
		
		var generated_action: BaseAction = ActionGenerator.generate_consumable(card_play_request)
		var _action_interceptor_processors: Array[ActionInterceptorProcessor] = generated_action._intercept_action([null], true)
		
		for action_interceptor_processor: ActionInterceptorProcessor in _action_interceptor_processors:
			intercepted_action_results["consumable_use_text"] = action_interceptor_processor.get_shadowed_action_values("consumable_use_text", consumable_use_text)
			intercepted_action_results["consumable_use_disabled"] = action_interceptor_processor.get_shadowed_action_values("consumable_use_disabled", consumable_use_disabled)
			intercepted_action_results["consumable_energy_cost"] = action_interceptor_processor.get_shadowed_action_values("consumable_energy_cost", consumable_energy_cost)
	
	
	return intercepted_action_results
