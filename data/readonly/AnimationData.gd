## Read only data for defining a set of animations and the state machine between them.
## These are converted to AnimatedTextures on runtime and cached internally.
## See ActionPlayAnimation, ActionAttackGenerator and EnemyIntentData
## NOTE: As AnimatedTexture is currently DEPRECATED due to performance reasons
## but no other simple alternatives exist, I am using them.
## You may wish to refactor this in the future if such alternative is implemented.
extends SerializableData
class_name AnimationData

## Default framerate for animations if none are specified.
const DEFAULT_FRAME_RATE: float = 5.0
## Default framerate for vfx animations if none are specificed
const DEFAULT_VFX_FRAME_RATE: float = 30.0

# standard animation names used by enemies and player
const ANIMATION_NONE: String = ""
const ANIMATION_IDLE: String = "animation_idle"
const ANIMATION_ATTACK: String = "animation_attack" # used by ActionAttackGenerator
const ANIMATION_DEATH: String = "animation_death"

# standard name used by VFX animations
const ANIMATION_VFX: String = "animation_default"


## Maps an animation name to the external texture file paths and other meta data used to generate it.
## These are generated and cached via FileLoader on runtime and stored in animations.
## Optional animation data that can be supplied to set up animation trees.
## By default animations will loop and play at a standard speed scale
@export var _animation_data: Dictionary[String, Variant] = {
	#"animation_name_1":
		#{
			# the external file paths for the frames of the animations
			#"animation_texture_file_paths": [],
			# how long each frame takes
			#"animation_frame_durations": [],
			## the name of the next animation when this one finishes.
				## if empty, no animation will play and it will stop on the last frame (aka one shot)
				## if the same as the current animation, it will set this animation to loop (default behavior)
				## if a different animation, it will switch to that one when finished
			#"animation_next_animation_name": "animation_name_1",
			#"animation_speed_scale": 1.0
		#}
}

## Will offset animation by this much
@export var animation_offset: Vector2 = Vector2()
## Applies a random offset within this range to any AnimatedCombatEffect
@export var animation_vfx_offset_random: int = 10

## Generated from animation_texture_file_paths and animation_meta_data on runtime
var animations: SpriteFrames = null

## Helper method for adding animations in one line.
## NOTE: generate_animations() must be done after calling this. This is already done automatically
## on game start for all AnimationData.
func add_animation(animation_name: String, animation_next_animation_name: String, animation_frame_texture_paths: Array[String], animation_fps: float = DEFAULT_FRAME_RATE) -> void:
	_animation_data[animation_name] = {
		"animation_texture_file_paths": animation_frame_texture_paths,
		"animation_next_animation_name": animation_next_animation_name,
		"animation_fps": animation_fps,
		}
	if animations == null:
		animations = SpriteFrames.new()
	
	# generate animation
	animations.remove_animation(animation_name)
	animations.add_animation(animation_name)
	for texture_path: String in animation_frame_texture_paths:
		var texture: ImageTexture = FileLoader.load_texture(texture_path)
		animations.add_frame(animation_name, texture)
	
	# set loop if next anim is same as this one
	animations.set_animation_loop(animation_name, false)
	if animation_next_animation_name == animation_next_animation_name:
		animations.set_animation_loop(animation_name, true)
	
	# set anim fps
	animations.set_animation_speed(animation_name, animation_fps)

## A boilerplate data generation method that saves writing a bunch of method calls.
## NOTE: If you just supply idle_animation_frames (typically 1 frame), the others will be copied. Useful for getting
## the bare minimum animation functionality in.
func add_combatant_animations(idle_animation_frames: Array[String], attack_animation_frames: Array[String] = idle_animation_frames, death_animation_frames: Array[String] = idle_animation_frames) -> void:
	add_animation(AnimationData.ANIMATION_IDLE, AnimationData.ANIMATION_IDLE, idle_animation_frames)
	add_animation(AnimationData.ANIMATION_ATTACK, AnimationData.ANIMATION_IDLE, attack_animation_frames)
	add_animation(AnimationData.ANIMATION_DEATH, AnimationData.ANIMATION_NONE, death_animation_frames)


func add_vfx_animations(animation_frames: Array[String], vfx_random_offset: int = 10, frame_rate = DEFAULT_VFX_FRAME_RATE) -> void:
	add_animation(AnimationData.ANIMATION_VFX, AnimationData.ANIMATION_NONE, animation_frames, frame_rate)
	animation_vfx_offset_random = vfx_random_offset

func get_next_animation_name(current_animation_name: String) -> String:
	if not _animation_data.has(current_animation_name):
		return ANIMATION_NONE # no further animation
	
	var animation_data: Dictionary = _animation_data.get(current_animation_name)
	var animation_next_animation_name: String = animation_data.get("animation_next_animation_name", current_animation_name)
	return animation_next_animation_name

## Takes animation data and loads textures/animations. Done on game start.
func regenerate_animations() -> void:
	animations = SpriteFrames.new()
	for animation_name: String in _animation_data:
		# get animation params
		var animation_data: Dictionary = _animation_data.get(animation_name)
		
		var animation_texture_file_paths: Array[String] = []
		animation_texture_file_paths.assign(animation_data.get("animation_texture_file_paths", []))
		var animation_frame_durations: Array[float] = []
		animation_frame_durations.assign(animation_data.get("animation_frame_durations", []))
		var animation_next_animation_name: String = animation_data.get("animation_next_animation_name", animation_name)
		var animation_fps: float = animation_data.get("animation_fps", DEFAULT_FRAME_RATE)
		
		# generate animation
		animations.add_animation(animation_name)
		for i: int in len(animation_texture_file_paths):
			var texture_path: String = animation_texture_file_paths[i]
			var frame_duration: float = 1.0
			if len(animation_frame_durations) > i:
				frame_duration = animation_frame_durations[i]
			var texture: ImageTexture = FileLoader.load_texture(texture_path)
			animations.add_frame(animation_name, texture, frame_duration)
		
		# set loop if next anim is same as this one
		animations.set_animation_loop(animation_name, false)
		if animation_name == animation_next_animation_name:
			animations.set_animation_loop(animation_name, true)
		
		# set anim fps
		animations.set_animation_speed(animation_name, animation_fps)
		

func _get_native_properties() -> Dictionary:
	return {
		"animation_offset": Vector2(),
		}
