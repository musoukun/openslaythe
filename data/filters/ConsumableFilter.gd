## A (usually temporary) object used to filter down an initial set of consumables.
## These are used for things like consumable drafts or consumables that generate random consumables.
## Supports method chaining. ex: ConsumableFilter.new(consumables).filter_1().filter_2().convert_to_consumable_prototypes()
## NOTE: For very large sets of consumables, you may wish to cache the ConsumableFilter with cache_filter() and reuse it.
extends RefCounted
class_name ConsumableFilter

var filtered_consumables: Array[ConsumableData] = []	# consumables after filters have been applied

## Maintains all filtered_consumables consumable_object_ids as a Set of keys for fast .has() lookups.
## Value is always null for each key.
var filtered_consumable_unique_object_ids: Dictionary[String, Variant]

## When cached, filtered_consumables cannot be mutated with filters, essentially locking the output
var cached: bool = false

### Start of Chain

## NOTE: If you do note provide an input consumableset, the default is to use the read only consumableset of
## ALL consumables in game. This is not only non-performant when many filters need to be applied, but
## the end result of the filter chain will still be the read-only consumables. You will need to finish
## the chain with convert_to_consumable_prototypes() or convert_to_consumable_object_ids() or risk mutating that data.
func _init(input_consumableset: Array[ConsumableData] = Global.get_all_consumables(), input_read_only_consumable_object_ids: Array[String] = []):
	filtered_consumables = input_consumableset
	# if an empty consumableset is provided, try to generate one using given ids
	# of read only consumable templates
	if len(input_consumableset) == 0:
		for input_consumable_object_id: String in input_read_only_consumable_object_ids:
			var consumable_data: ConsumableData = Global.get_consumable_data(input_consumable_object_id)
			input_consumableset.append(consumable_data)
			filtered_consumable_unique_object_ids[consumable_data.object_id] = null
	else:
		for consumable_data: ConsumableData in input_consumableset:
			filtered_consumable_unique_object_ids[consumable_data.object_id] = null

### Filters

func filter_colors(consumable_color_ids: Array[String] = [], include: bool = true) -> ConsumableFilter:
	if cached:
		return self
	if len(consumable_color_ids) == 0:
		return self
	
	var returned_consumables: Array[ConsumableData] = []
	var returned_consumable_object_ids: Dictionary[String, Variant] = {}
	
	for consumable_data in filtered_consumables:
		var consumable_has_color: bool = consumable_color_ids.has(consumable_data.consumable_color_id)
		
		if consumable_has_color == include:
			returned_consumables.append(consumable_data)
			returned_consumable_object_ids[consumable_data.object_id] = null
	
	filtered_consumables = returned_consumables
	filtered_consumable_unique_object_ids = returned_consumable_object_ids
	return self

## Throttles the filtered consumables to the first N results. -1 for no filtering
func first_results(consumable_amount: int = -1) -> ConsumableFilter:
	if cached:
		return self
	if consumable_amount <= 0:
		return self
		
	filtered_consumables = filtered_consumables.slice(0, consumable_amount)
	return self

### Include

## Forcefully includes consumables into the consumable filter results, to be used after all filters have been
## applied. Only useful if you're using read only consumable inputs
func include_consumable_object_ids(consumable_read_only_object_ids: Array[String]) -> ConsumableFilter:
	if cached:
		return self
	
	for consumable_read_only_object_id: String in consumable_read_only_object_ids:
		if not filtered_consumable_unique_object_ids.has(consumable_read_only_object_id):
			var consumable_data: ConsumableData = Global.get_consumable_data(consumable_read_only_object_id)
			filtered_consumables.append(consumable_data)
			filtered_consumable_unique_object_ids[consumable_data.object_id] = null
	
	return self


### Cache

## Prevents filter from being further mutated and caches it under a given id
func cache_filter(consumable_filter_cache_id: String) -> ConsumableFilter:
	cached = true
	Global.cache_consumable_filter(consumable_filter_cache_id, self)
	return self

### End of Chain

## Done at the end of chain to convert the remaining consumables into an id list. Allows duplicates.
func convert_to_consumable_object_ids() -> Array[String]:
	# done at the end of a filter chain to convert the remaining consumables into an id list
	var consumable_object_ids: Array[String] = []
	for consumable_data in filtered_consumables:
		consumable_object_ids.append(consumable_data.object_id)
	return consumable_object_ids

func convert_to_unique_consumable_object_ids() -> Array[String]:
	return filtered_consumable_unique_object_ids.keys().duplicate(true) # duplicated to allow immediate mutation/shuffling, as is usually the case
