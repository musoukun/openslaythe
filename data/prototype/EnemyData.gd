## Prototype data for an enemy combatant.
## Defines things like health ranges, enemy attack patterns, and 
extends PrototypeData
class_name EnemyData

@export var enemy_name: String = ""
@export var enemy_texture_path: String = "external/sprites/enemies/enemy_blue_small.png"
## Maps to a given AnimationData object id
@export var enemy_animation_id: String = ""

## Enemy's current health. If this reaches 0 the enemy will die.
@export var enemy_health: int = 20
## Maximum enemy health. Cannot exceed this value. This value is determined based on
## enemy_health_max_random_lower and enemy_health_max_random_upper when the enemy spawns.
@export var enemy_health_max: int = 20

## Upper limit to the max health this enemy type can spawn with.
@export var enemy_health_max_random_lower: int = 20
## Upper limit to the max health this enemy type can spawn with.
@export var enemy_health_max_random_upper: int = 25

## How much block the enemy has. For EnemyData prototypes, it defines how much block the enemy
## starts with on turn 1.
@export var enemy_block: int = 0

## Action data payload for what to do when the enemy dies.
@export var enemy_actions_on_death: Array[Dictionary] = []

enum ENEMY_TYPES {STANDARD, MINIBOSS, BOSS}
@export var enemy_type: int = ENEMY_TYPES.STANDARD
## Minion enemies do not need to be killed for combat to end.
@export var enemy_is_minion: bool = false

## Maps status effect ids to charge count at start of combat.
@export var enemy_initial_status_effects: Dictionary[String, int] = {}

## Time in seconds between each enemy attack.
const ENEMY_ATTACK_DELAY: float = 0.5

#region Intent State

## initial state which it will iterate from on combat start.
## The current enemy's attack state. Defines an object_id in enemy_intents. Always starts at initial
## state which is used to randomly cycle to other states on combat start.
var enemy_intent_current_id: String = EnemyIntentData.INTENT_INITIAL

## A debug intent that shows pretty obviously when an intent is missing or badly overridden.
## CRITICAL: Avoid having this used because it contains numbers too funny for human consumption.
static var MISSING_INTENT: EnemyIntentData = EnemyIntentData.new("INTENT_DUMMY", 0, 1, 69, "", 420, "", {"INTENT_DUMMY": 1}, [{Scripts.ACTION_DEBUG_LOG: {"log_message": "YOU DONE GOOFED", "log_message_color_html": Color.PINK.to_html(false)}}])

## Embedded read only enemy intents. Forms a random weighted directed graph.
## "intent_initial" is a dummy intent used to randomly select a first intent at the start of combat,
## and should always be supplied with weights for the next intents.
## NOTE: You can easily add these via add_intent_state() during data generation process.
## NOTE: If you want enemies to change their attack patterns based on difficulty, you may
## use intent overriding which hotswaps intents with different ones without having to redefine the entire
## behavior tree. This happens automatically when you specify 2 intents with the same provided ID in their
## constructor but one has a higher difficulty.
@export var enemy_intents: Dictionary[String, EnemyIntentData] = {
	# EnemyIntentData.INTENT_INITIAL: EnemyIntentData.new(EnemyIntentData.INTENT_INITIAL, ...)
	# "intent_attack": EnemyIntentData.new("intent_attack", 0 ...)
	# "intent_attack": EnemyIntentData.new("intent_attack", 3 ...) # you can use intent overriding to swap intent_low_difficulty_attack for intent_high_difficulty_attack at difficulty 3
}
#endregion

#region Difficulty
## Maps a difficulty level to a set of properties and their values.
## NOTE: Keys are difficulty level integers stored as strings to make them json friendly.
## NOTE: Difficulty based max health should be defined using add_health_bounds().
## NOTE: Difficulty based enemy intent should be defined using add_intent_state().
@export var enemy_difficulty_to_enemy_modfiers: Dictionary[String, Dictionary] = {
	#"2": {
		# Gives enemy starting block on turn 1 at difficulty 2
		#"enemy_block": 10,
	#},
	
}

## Internal cache for each individual enemy, generated after apply_enemy_difficulty_modifiers().
## This allows you to essentially store different intents and hotswap them without having to do
## a bunch of tedious work defining an entirely new attack tree.
## You simply define multiple of the same intent via add_intent_state()
## and the rest of the graph will work perfectly fine.
## internal cache built during apply_enemy_difficulty_modifiers().
var _intent_override_cache: Dictionary[String, String] = {
	# "intent_id_1": "intent_id_2"
}

## Applies all modifiers acrosss all difficulties up to the player's difficulty for this enemy.
## This will overwrite each listed property of EnemyData with new values.
## It will also populate _intent_override_cache for difficulty attack overrides.
func apply_enemy_difficulty_modifiers():
	var player_run_difficulty_level: int = Global.player_data.player_run_difficulty_level
	for difficulty_level: int in (player_run_difficulty_level + 1):
		# get modifiers for each difficulty level and apply them
		var difficulty_as_string: String = str(difficulty_level)
		var enemy_difficulty_modifiers: Dictionary = enemy_difficulty_to_enemy_modfiers.get(difficulty_as_string, {})
		for property_name: String in enemy_difficulty_modifiers:
			set(property_name, enemy_difficulty_modifiers[property_name])
	
	for enemy_intent_data: EnemyIntentData in enemy_intents.values():
		# override intents at higher difficulty levels
		if not _intent_override_cache.has(enemy_intent_data.enemy_intent_overrides_id):
			_intent_override_cache[enemy_intent_data.enemy_intent_overrides_id] = enemy_intent_data.object_id
		else:
			var old_enemy_intent_id: String = _intent_override_cache[enemy_intent_data.enemy_intent_overrides_id]
			var old_enemy_intent_data: EnemyIntentData = enemy_intents[old_enemy_intent_id]
			if old_enemy_intent_data.enemy_intent_difficulty_level < enemy_intent_data.enemy_intent_difficulty_level:
				_intent_override_cache[enemy_intent_data.enemy_intent_overrides_id] = enemy_intent_data.object_id

## Helper method. Randomizes the enemy's max health, used during the spawning process.
## For best results, this should be called after apply_enemy_difficulty_modifiers() with modifiers
## to enemy_health_max_random_lower and enemy_health_max_random_upper.
func randomize_health(reset_health_to_new_max: bool = true) -> void:
	var rng_enemy_health: RandomNumberGenerator = Global.player_data.get_player_rng("rng_enemy_health")
	enemy_health_max = rng_enemy_health.randi_range(enemy_health_max_random_lower, enemy_health_max_random_upper)
	if reset_health_to_new_max:
		enemy_health = enemy_health_max

#endregion

#region Intents

## Makes the enemy cycle to next intent in enemy's intent graph. This is typically called at the start
## of the player's turn, and the first turn uses the INTENT_INITIAL state to determine a random
## starting attack.
func cycle_next_intent_state() -> void:
	var current_intent_state: EnemyIntentData = get_current_intent()
	var rng_enemy_attack_patterns: RandomNumberGenerator = Global.player_data.get_player_rng("rng_enemy_attack_patterns")
	
	# no weights means it can't cycle.
	# will stay on current state.
	if len(current_intent_state.enemy_intent_next_intent_weights) == 0:
		DebugLogger.log_warning("EnemyData: No next intent weights defined for {0}".format([enemy_intent_current_id]))
		
	# randomly get next intent. Requires a typecast
	var weights: Dictionary[Variant, int] = {}
	weights.assign(current_intent_state.enemy_intent_next_intent_weights)
	enemy_intent_current_id = Random.get_weighted_selection(rng_enemy_attack_patterns, weights)

## Gets the enemy's current attack intent, after being overridden.
func get_current_intent() -> EnemyIntentData:
	var overridden_intent_id: String = _get_overridden_intent_id(enemy_intent_current_id)
	var current_intent_state: EnemyIntentData = enemy_intents.get(overridden_intent_id, null)
	# check if intent is defined
	if current_intent_state == null:
		# undefined intent; overriding may have failed, try to fall back to non-overridden state
		current_intent_state = enemy_intents.get(enemy_intent_current_id, null)
		if current_intent_state == null:
			# both overridden and non-overridden intents (which can be the same one) are missing
			DebugLogger.log_error("EnemyData: No intent defined for {0}. Replacing with debug intent...".format([enemy_intent_current_id]))
			breakpoint
			add_intent_state([MISSING_INTENT]) # add a debug intent to this enemy so it doesn't continually fail
			enemy_intent_current_id = MISSING_INTENT.object_id
			return MISSING_INTENT
		else:
			# issue with intent overriding; use the non-overridden version
			DebugLogger.log_error("EnemyData: Intent overriding failed for {0}. Falling back to {1}".format([overridden_intent_id, enemy_intent_current_id]))
			breakpoint
	
	return current_intent_state

## This method will convert intent object_ids, allowing for intent overriding. Will return
## the same intent if not overridden.
func _get_overridden_intent_id(original_intent_id: String) -> String:
	return _intent_override_cache.get(original_intent_id, original_intent_id)

#endregion

#region Data Generation
## Helper method for simplifying data generation code for enemy intents for prototypes.
## NOTE: While this can technically be used to add multiple intent states of different kinds,
## as a convention for readability you should group up the same intents and their overrides under one method call.
func add_intent_state(_enemy_intents: Array[EnemyIntentData]) -> void:
	for enemy_intent: EnemyIntentData in _enemy_intents:
		enemy_intents[enemy_intent.object_id] = enemy_intent

## Helper method. Makes setting health into a one liner during data generation code for enemy prototypes.
## If difficulty_level is not 0, it will instead update the difficulty modifiers for health
func add_health_bounds(health_lower: int, health_upper: int, difficulty_level: int = 0) -> void:
	if difficulty_level <= 0:
		enemy_health_max_random_lower = health_lower
		enemy_health_max_random_upper = health_upper
	else:
		# take (possibly) existing difficulty modifiers and insert the new health values into it
		var difficulty_as_string: String = str(difficulty_level)
		var modifiers: Dictionary = enemy_difficulty_to_enemy_modfiers.get(difficulty_as_string, {})
		modifiers = modifiers.duplicate()
		modifiers["enemy_health_max_random_lower"] = health_lower
		modifiers["enemy_health_max_random_upper"] = health_upper
		enemy_difficulty_to_enemy_modfiers[difficulty_as_string] = modifiers

## Helper method to add boilerplate animations with the least amount of code possible.
## Automatically registers the animation in Global.
func add_standard_animations(idle_animation_frames: Array[String], attack_animation_frames: Array[String] = idle_animation_frames, death_animation_frames: Array[String] = idle_animation_frames) -> AnimationData:
	var animation_name: String = "animation_enemy_{0}".format([enemy_name.to_snake_case()]) # auto generate animation name from enemy name
	var animation_id: String = "animation_{0}".format([object_id.to_snake_case()]) # auto generate animation id from enemy id
	var animation: AnimationData = AnimationData.new(animation_id)
	enemy_animation_id = animation_id
	animation.add_combatant_animations(
		idle_animation_frames,
		attack_animation_frames,
		death_animation_frames
		)
	
	Global.register_rod(animation)
	
	return animation

#endregion
