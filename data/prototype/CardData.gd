## Prototyped data object containing all information on a card
extends PrototypeData
class_name CardData

var parent_card: CardData = null	# the parent card in the player's true deck that this one was copied from. This is used for cards copied from the player's deck in combat. This is not the prototype the card is made from. This should only ever be one layer deep at most. Mainly used for meta scaling cards.
@export var card_name: String = ""
@export var card_description: String = ""
@export var card_texture_path: String = ""
@export var card_keyword_object_ids: Array[String] = [] # keywords (mechanics with tooltips) displayed when this card is hovered over
@export var card_color_id: String = "color_green"

# Card Energy
## The raw energy cost of the card before modifiers. Generally shadowed by the others.
## See: get_card_energy_cost() for how energy costs are determined.
@export var card_energy_cost: int = 1 : set = set_card_energy_cost
@export var card_energy_cost_until_played: int = -1	: set = set_card_energy_cost_until_played # can shadow card_energy_cost. -1 means no shadowing
@export var card_energy_cost_until_turn: int = -1	: set = set_card_energy_cost_until_turn # can shadow card_energy_cost. -1 means no shadowing
## The energy cost of the card for the reamining duration of combat.
## Can shadow card_energy_cost. -1 means no shadowing.
## Allows for changing general card cost while keeping original card_energy_cost value.
@export var card_energy_cost_until_combat: int = -1	: set = set_card_energy_cost_until_combat
# variable energy cost
@export var card_energy_cost_is_variable: bool = false	# if card costs all energy to play, for X cost cards
@export var card_energy_cost_variable_upper_bound: int = -1	# allows an upper bound on energy input into an X cost card. -1 for no limit

## Determines how a card should be shuffled in the deck on combat start.
## These form buckets which are individually shuffled then combined.
## Typically 1,0,-1. Positive values drawn first, negative values go to bottom of draw pile.
@export var card_first_shuffle_priority: int = 0
## The sorting priority of where a card will end after the first shuffle. Works same as card_first_shuffle_priority.
@export var card_reshuffle_priority: int = 0

## The weighting of a card within buckets defined by card_first_shuffle_priority or card_reshuffle_priority.
## This allows for cards to be "light" or "heavy" within a shuffle. The weighting is defined as a ratio
## Cannot be 0.0 or negative or the math police will come for you.
@export var card_shuffle_weighting: float = 1.0

# Card Type
enum CARD_TYPES {ATTACK, SKILL, POWER, STATUS, CURSE}
const STANDARD_CARD_TYPES: Array[int] = [CARD_TYPES.ATTACK, CARD_TYPES.SKILL, CARD_TYPES.POWER]
@export var card_type: int = CARD_TYPES.ATTACK

# Card Rarity
enum CARD_RARITIES {BASIC, COMMON, UNCOMMON, RARE, GENERATED}
const STANDARD_CARD_RARITIES: Array[int] = [CARD_RARITIES.COMMON, CARD_RARITIES.UNCOMMON, CARD_RARITIES.RARE]
@export var card_rarity: int = CARD_RARITIES.COMMON

## Make false to prevent cards with this object_id from appearing in card packs. This only takes effect
## once during initial runtime and is useful to prevent cards from appearing without giving them GENERATED rarity
## or explicitly listing them by ID in packs. Essentially just an extensibility feature with niche application.
## See: CardPackData.create_card_pack_card_filter()
@export var card_appears_in_card_packs: bool = true

# Card Play Flags
## Which pile the card goes to when the card is played. Typically this will be
## either DISCARD_PILE for standard cards, EXHAUST_PILE for cards that exhaust, and
## BANISH_PILE for single use cards.
## Combines with card_play_destination_strategy.
@export var card_play_destination: String = HandManager.DISCARD_PILE
## Combined with card_play_destination to determine exactly where a card will wind up.
## Eg the top of the discard pile.
@export var card_play_destination_strategy: int = HandManager.PILE_INSERTION_STRATEGIES.TOP

## Which pile the card goes when the turn ends. Typically this will be
## either DISCARD_PILE for standard cards and EXHAUST_PILE for ethereal cards
@export var card_end_of_turn_destination: String = HandManager.DISCARD_PILE
## Works similar to card_play_destination_strategy.
## NOTE: To make a card retain, it should be 
@export var card_end_of_turn_destination_strategy: int = HandManager.PILE_INSERTION_STRATEGIES.TOP
## Make false to make the card unplayable regardless of passing card_play_validators.
@export var card_is_playable: bool = true

## Flag that determines whether the card exhausts if in hand at the end of the turn.
## NOTE: This will override card_end_of_turn_destination_strategy.
## NOTE: A card can also be considered ethereal without this flag if card_end_of_turn_destination_strategy
## is EXHAUST_PILE. See: is_card_ethereal().
@export var card_is_ethereal = false
## If the card innately stays in hand end of turn and does not discard. Will also trigger
## card_retain_actions as a result.
## NOTE: If ethereal, a retained card will still execute retain actions before exhausting.
## NOTE: This can also be achieved via card_end_of_turn_destinion == HAND, but some edge cases
## such as a card being both ethereal AND retaining can not be achieved without two flags.
@export var card_is_retained: bool = false
## Card requires user to select a target in order to play it. The selected target will be
## supplied to the CardPlayRequest.
@export var card_requires_target: bool = true

# Card Values
## Calues on the card like attack/block amount.
## These are fallback values used by the card's actions and can be modified.
## See BaseAction.get_action_value() for more.
## Display them in card_description with [card_value_name], eg [block] or [damage].
@export var card_values: Dictionary = {}
## Adds ability to inject more values/custom values to intercept beyond basic
## automatic damage/block calculations when displaying card descriptions.
@export var card_description_preview_overrides: Array[Array] = [
	# ["damage", Scripts.ACTION_ATTACK],	# 2 parameters: value_name + intercecpted action
	# ["damage", Scripts.ACTION_ATTACK, "damage_1"], # Optional third parameter for mapping custom values
]

### Card Actions
@export var card_play_actions: Array[Dictionary] = [
	#{
	#Scripts.ACTION_ATTACK_GENERATOR: {"damage": 5, "number_of_attacks": 2, "time_delay": 0.0}
	#}
]
@export var card_discard_actions: Array[Dictionary] = []	# actions that trigger when card is manually discarded
@export var card_end_of_turn_actions: Array[Dictionary] = []	# actions that trigger when the card is in hand end of turn
@export var card_exhaust_actions: Array[Dictionary] = []	# actions that trigger when card is exhausted
@export var card_draw_actions: Array[Dictionary] = []	# actions that trigger when card is drawn and added to hand
@export var card_retain_actions: Array[Dictionary] = []	# actions that trigger when card is retained at the end of turn
@export var card_right_click_actions: Array[Dictionary] = []	# actions that trigger when card is right clicked while in hand
@export var card_initial_combat_actions: Array[Dictionary] = []	# actions that trigger at the start of combat for each card in the deck

@export var card_add_to_deck_actions: Array[Dictionary] = []	# actions that trigger when card is added to player's permanent deck 
@export var card_remove_from_deck_actions: Array[Dictionary] = []	# actions that trigger when card is removed from player's permanent deck
@export var card_transform_in_deck_actions: Array[Dictionary] = []	# actions that trigger when card is transformed in player's permanent deck

## Validators required for the card to be playable. Will make the card glow if all pass.
@export var card_play_validators: Array[Dictionary] = [
	#{"validator_script_path.gd": {"validator_value_1": Variant}}
]
## Validators that make the card glow. If empty then card_play_validators will be used for glow.
## Useful for cards with bonus conditional effects.
@export var card_glow_validators: Array[Dictionary] = []

## Maps CardDecoratorData object_ids to the values of the decorator if any.
## NOTE: These values are used as behavioral parameters into the BaseCardDecorator script, allowing
## for configurable/reusable scripts between multiple decorators. This is not the same as
## CardDecorator.card_decorator_value_improvements.
@export var card_decorators: Dictionary[String, Dictionary] = {
	# decorator_object_id: {"param_1": "value"}
}

## The maximum number of visible decorators a card can have. Generally you'll want this to be
## 1-3. Invisible decorators will not be counted for max slots.
const CARD_MAX_DECORATOR_SLOTS: int = 1

## Optional tags that can be applied to a card to filter them arbitrarily, or apply them in combat
## as some kind of invisible token. Used in ValidatorCardTag.
## Updated via add_card_tag() and remove_card_tag().
@export var card_tags: Array[String] = [
	#"tag_<something>"
]

# Upgrades
## The number of times the card has been upgraded.
@export var card_upgrade_amount: int = 0
## Max number of times the card can be upgraded.
@export var card_upgrade_amount_max: int = 1
## Applies .set() to the CardData's properties on first upgrade.
## NOTE: This is not the card_values, but the properties of CardData itself such as card_is_ethereal
## or card_play_actions.
@export var card_first_upgrade_property_changes: Dictionary[String, Variant] = {
	
}
## Updates the card_values to these on the first upgrade.
@export var card_first_upgrade_value_changes: Dictionary[String, Variant] = {
	
}
## Applies improve_card_values() each upgrade.
@export var card_upgrade_value_improvements: Dictionary[String, int] = {
	
}

# Deck Flags
## If the card cannot be removed from the permanent deck. Does nothing by itself, this should
## be enforced through validators in card picking actions. See: ValidatorCardRemovableFromDeck
@export var card_unremovable_from_deck: bool = false
## If the card cannot be transformed from the permanent deck. Does nothing by itself, this should
## be enforced through validators in card picking actions. See: ValidatorCardTransformableFromDeck
@export var card_untransformable_from_deck: bool = false

func _to_string():
	return get_card_name()

## Returns a card's name, modified by the number of times it's been upgraded.
func get_card_name() -> String:
	if card_upgrade_amount > 0:
		if card_upgrade_amount > 1:
			return card_name + "+" + str(card_upgrade_amount - 1)
		else:
			return card_name + "+"
	else:
		return card_name

#region Card Energy
## Returns the card's modified energy cost, which allows for energy types to be applied in a hierarchy.
## This is particularly useful in combat when cards can have their energy changed for different
## durations which can overshadow each other.
func get_card_energy_cost(shadow_energy_cost: bool = true, variable_cost_is_zero: bool = false) -> int:
	# variable cost cards may be treated as either all energy or zero
	if card_energy_cost_is_variable:
		if variable_cost_is_zero:
			return 0
		else:
			if card_energy_cost_variable_upper_bound < 0:
				# variable cost cards with no upper bound
				return Global.player_data.player_energy
			else:
				return max(min(Global.player_data.player_energy, card_energy_cost_variable_upper_bound), 0)
	# energy shadowing. This operates in a hierarchy that short circuits.
	if shadow_energy_cost:
		if card_energy_cost_until_played > -1:
			return card_energy_cost_until_played
		if card_energy_cost_until_turn > -1:
			return card_energy_cost_until_turn
		if card_energy_cost_until_combat > -1:
			return card_energy_cost_until_combat
	# no shadowing, simply return the card's cost
	return card_energy_cost

func set_card_energy_cost(energy_cost: int) -> void:
	var cost: int = max(0, energy_cost)
	if card_energy_cost != cost:
		card_energy_cost = cost
		Signals.card_properties_changed.emit(self)

func set_card_energy_cost_until_played(energy_cost: int) -> void:
	if energy_cost != card_energy_cost_until_played:
		card_energy_cost_until_played = energy_cost
		Signals.card_properties_changed.emit(self)

func set_card_energy_cost_until_turn(energy_cost: int) -> void:
	if energy_cost != card_energy_cost_until_turn:
		card_energy_cost_until_turn = energy_cost
		Signals.card_turn_energy_changed.emit(self)

func set_card_energy_cost_until_combat(energy_cost: int) -> void:
	if energy_cost != card_energy_cost_until_combat:
		card_energy_cost_until_combat = energy_cost
		Signals.card_properties_changed.emit(self)
#endregion

## Gets the card description, including built in card related keywords
func get_card_description() -> String:
	var modified_card_description: String = card_description
	
	if card_first_shuffle_priority > 0:
		modified_card_description = "[color=orange]Top Deck[/color]\n" + modified_card_description
	if card_first_shuffle_priority < 0:
		modified_card_description = "[color=orange]Bottom Deck[/color]\n" + modified_card_description
	if not card_is_playable:
		modified_card_description = "[color=orange]Unplayable[/color]\n" + modified_card_description
	if card_is_retained:
		modified_card_description = "[color=orange]Retain[/color]\n" + modified_card_description
	if is_card_ethereal():
		modified_card_description = "[color=orange]Ethereal[/color]\n" + modified_card_description
	if does_card_exhaust():
		modified_card_description = modified_card_description + "\n[color=orange]Exhaust[/color]"
	if does_card_banish():
		modified_card_description = modified_card_description + "\n[color=orange]Banish[/color]"
	
	return modified_card_description

## Generates an ActionCardPlay, intercepts it, then returns the intercepted results in dictionary form.
## This cuts down on a lot of messy code used in getting intercepted values used for playing a card
## or displaying its energy.
## NOTE: Godot pls to add structs kthx
func get_card_play_intercepted_action_results(selected_enemy: Enemy = null) -> Dictionary[String, Variant]:
	var intercepted_action_results: Dictionary[String, Variant] = {}
	
	# will only intercept during a run for performance reasons
	if Global.is_run and HandManager.player_hand.has(self):
		var card_play_request: CardPlayRequest = HandManager.create_card_play_request(self, selected_enemy, false, false) # generate fake request
		var generated_action: BaseAction = ActionGenerator.generate_card_play(card_play_request)
		var _action_interceptor_processors: Array[ActionInterceptorProcessor] = generated_action._intercept_action([selected_enemy], true)
		
		for action_interceptor_processor: ActionInterceptorProcessor in _action_interceptor_processors:
			# This intercepted value will normally default to CardData.get_card_energy_cost(). You may intercept
			# this to modify how much the card will actually cost. A common example of this is a status
			# making the next card play cost 0.
			intercepted_action_results["card_energy_cost"] = action_interceptor_processor.get_shadowed_action_values("card_energy_cost", self.get_card_energy_cost(true, true))
			# Allows X cost cards to have a modifiable upper bound
			intercepted_action_results["card_energy_cost_variable_upper_bound"] = action_interceptor_processor.get_shadowed_action_values("card_energy_cost_variable_upper_bound", self.card_energy_cost_variable_upper_bound)
			# This flag can be intercepted to make unplayable cards playable or vice versa. This does not factor in
			# other things like validators.
			intercepted_action_results["card_is_playable"] = action_interceptor_processor.get_shadowed_action_values("card_is_playable", self.card_is_playable)
			# This can be used to ignore validators when playing the card
			intercepted_action_results["card_ignores_validators"] = action_interceptor_processor.get_shadowed_action_values("card_ignores_validators", false)

	return intercepted_action_results

#region Card Values and Properties
## Overwrites card's properties using .set().
## This affect's CardData's fields. Not the same as changing card_values.
func set_card_properties(card_properties: Dictionary[String, Variant]) -> void:
	for property_name: String in card_properties:
		set(property_name, card_properties[property_name])
	Signals.card_properties_changed.emit(self)

## Iterates over the card's card_values, updating them to new given values.
func update_card_values(_new_card_values: Dictionary[String, Variant]) -> void:
	var new_card_values: Dictionary[String, Variant] = _new_card_values.duplicate(true) # duplicate dict to ensure no reference problems
	for key_name: String in new_card_values:
		card_values[key_name] = new_card_values[key_name]
	Signals.card_properties_changed.emit(self)

## Iterate over the card's card_values, adding to them where necessary. Allows limited support for
## recursive data.
func improve_card_values(card_value_improvements: Dictionary[String, int]) -> void:
	for key_name: String in card_value_improvements.duplicate(true).keys():
		var improve_by_value: Variant = card_value_improvements[key_name] # get the modifier to improve the base value by
		if card_values.has(key_name):
			# add to existing value
			if improve_by_value is int:
				# add numbers to numbers
				card_values[key_name] = max(0, card_values[key_name] + improve_by_value	)
			if improve_by_value is Dictionary:
				# add all parallel dictionary keys
				if card_values[key_name] is Dictionary:
					for sub_key_name in improve_by_value:
						card_values[key_name][sub_key_name] = max(0, card_values[key_name][sub_key_name] + improve_by_value[sub_key_name])
		else:
			# overwrite non existing keys
			card_values[key_name] = improve_by_value
	
	Signals.card_properties_changed.emit(self)

## Clamps card_values to a given range.
## Format is {"card_value_name": [lower_bound, upper_bound]}
func clamp_card_values(card_value_bounds: Dictionary[String, Array]) -> void:
	for key_name: String in card_value_bounds.keys():
		if card_values.has(key_name):
			var card_value: int = card_values[key_name]
			var bounds: Array = card_value_bounds[key_name]
			if len(bounds) < 2:
				DebugLogger.log_error("Insufficient card clamp values. 2 integers expected for {0}. {1} given".format([key_name, len(bounds)]))
				continue
			var lower_bound: int = bounds[0]
			var upper_bound: int = bounds[1]
			card_value = clamp(card_value, lower_bound, upper_bound)
			card_values[key_name] = card_value
	
	Signals.card_properties_changed.emit(self)
#endregion

## Upgrades a card, overwriting properities and improving any card values.
func upgrade_card(upgrade_count: int = 1, bypass_upgrade_max = false) -> void:
	for _i in upgrade_count:
		# check if upgrade possible
		if (card_upgrade_amount < card_upgrade_amount_max) or bypass_upgrade_max:
			# perform first upgrade mutations
			if card_upgrade_amount == 0:
				# overwrite card_values on first upgrade
				update_card_values(card_first_upgrade_value_changes)
				# overwrite CardData's own properties on the first upgrade
				set_card_properties(card_first_upgrade_property_changes)
			
			# improve card values each upgrade
			improve_card_values(card_upgrade_value_improvements)
			card_upgrade_amount += 1
			
			Signals.card_upgraded.emit(self)

func transform_card(new_card_object_id: String) -> void:
	# transforms a card, overwriting the values of a card with that of a new card prototype id's values
	var old_uid: String = object_uid
	var card_data: CardData = Global.get_card_data_from_prototype(new_card_object_id)
	var exported_properties: Dictionary = card_data.get_serializable_properties()
	for property_name in exported_properties.keys():
		var property_value: Variant = exported_properties[property_name]
		set(property_name, property_value)
	
	object_uid = old_uid # preserve the uid between transforms
	Signals.card_transformed.emit(self)

func add_card_tag(card_tag: String) -> void:
	if not card_tags.has(card_tag):
		card_tags.append(card_tag)
func remove_card_tag(card_tag: String) -> void:
	card_tags.erase(card_tag)

## Gets the texture corresponding to the color for this card.
func get_card_color_texture_path() -> String:
	if card_color_id != "":
		var color_data: ColorData = Global.get_color_data(card_color_id)
		if color_data != null:
			return color_data.color_energy_icon_texture_path
	return ""

#region Decorators
## Applies a given decorator to this card. This will mutate the card with
## card_decorator_value_changes, card_decorator_value_improvements, and card_decorator_property_changes.
## It will also add actions before and after the card's actions, and add keywords.
## WARNING: decorator_script_values is used as a parameter for operating the BaseCardCardDecorator scripts. These values
## are different than values that are piped into the decorator's actions if they exist. Those are injected
## into the CardData.card_values via CardDecoratorData.card_decorator_value_improvements, which also happens in this method.
func add_card_decorator(card_decorator_id: String, decorator_script_values: Dictionary) -> void:
	if not card_decorators.has(card_decorator_id):
		card_decorators[card_decorator_id] = decorator_script_values
		
		# mutate card properties and card_values
		var card_decorator_data: CardDecoratorData = Global.get_card_decorator_data(card_decorator_id)
		set_card_properties(card_decorator_data.card_decorator_property_changes)
		improve_card_values(card_decorator_data.card_decorator_value_improvements)
		update_card_values(card_decorator_data.card_decorator_value_changes)
		
		# mutate card description
		card_description = card_decorator_data.card_decorator_pre_description + card_description +  card_decorator_data.card_decorator_post_description
		
		# mutate card actions, wrapping them before and after
		card_play_actions = card_decorator_data.card_decorator_post_play_actions + card_play_actions + card_decorator_data.card_decorator_pre_play_actions
		card_discard_actions = card_decorator_data.card_decorator_post_discard_actions + card_discard_actions + card_decorator_data.card_decorator_pre_discard_actions
		card_end_of_turn_actions = card_decorator_data.card_decorator_post_end_of_turn_actions + card_end_of_turn_actions + card_decorator_data.card_decorator_pre_end_of_turn_actions
		card_exhaust_actions = card_decorator_data.card_decorator_post_exhaust_actions + card_exhaust_actions + card_decorator_data.card_decorator_pre_exhaust_actions
		card_draw_actions = card_decorator_data.card_decorator_post_draw_actions + card_draw_actions + card_decorator_data.card_decorator_pre_draw_actions
		card_retain_actions = card_decorator_data.card_decorator_post_retain_actions + card_retain_actions + card_decorator_data.card_decorator_pre_retain_actions
		card_right_click_actions = card_decorator_data.card_decorator_post_right_click_actions + card_right_click_actions + card_decorator_data.card_decorator_pre_right_click_actions
		card_initial_combat_actions = card_decorator_data.card_decorator_post_initial_combat_actions + card_initial_combat_actions + card_decorator_data.card_decorator_pre_initial_combat_actions
		
		# add keywords
		for keyword_object_id: String in card_decorator_data.card_decorator_add_keyword_ids:
			if not card_keyword_object_ids.has(keyword_object_id):
				card_keyword_object_ids.append(keyword_object_id)
		# remove keywords
		for keyword_object_id: String in card_decorator_data.card_decorator_remove_keyword_ids:
			if card_keyword_object_ids.has(keyword_object_id):
				card_keyword_object_ids.erase(keyword_object_id)
		
		# sort the decorator keys so they appear on the Card in consistent order
		card_decorators.sort()
		
		# generate decorator addition actions and add them to the stack
		ActionGenerator.generate_decorator_actions(self, card_decorator_id)
		
		# signal the card has changed, which may trigger visual updates
		Signals.card_decorators_changed.emit(self)
		
## Removes a decorator from the card.
## WARNING: This is primarily only useful for decorators that do NOT mutate card_values or properties.
## Generally you should not remove decorators, and this method only exists for interface consistency.
func remove_card_decorator(card_decorator_id: String) -> void:
	card_decorators.erase(card_decorator_id)
	Signals.card_decorators_changed.emit(self)
	
## Returns if there are any empty decorator slots for visible decorators to appear in, defined by CARD_MAX_DECORATOR_SLOTS.
func get_card_has_empty_decorator_slots() -> bool:
	var visible_decorator_count: int = 0
	for card_decorator_id: String in card_decorators:
		var decorator_data: CardDecoratorData = Global.get_card_decorator_data(card_decorator_id)
		if decorator_data.is_decorator_visible():
			visible_decorator_count += 1
	
	if visible_decorator_count >= CARD_MAX_DECORATOR_SLOTS:
		return false
	return true

## Returns whether or not this card can have a given card decorator applied.
## Fails if the decorator already is applied, it can't be slotted, or it's invalid for this kind of card
func is_card_decorator_applicable(card_decorator_id: String) -> bool:
	if card_decorator_id == "":
		return false # decorator not defined
	if card_decorators.has(card_decorator_id):
		return false # decorator already applied, cannot be applied again
	
	var card_decorator_data: CardDecoratorData = Global.get_card_decorator_data(card_decorator_id)
	if card_decorator_data == null:
		return false # card decorator does not exist
	
	# check if this card appears in the given decorator's card pack
	if card_decorator_data.card_decorator_card_pack_id != "":
		var card_filter: CardFilter = Global.get_cached_card_filter(card_decorator_data.card_decorator_card_pack_id)
		if not card_filter.filtered_card_unique_object_ids.has(object_id):
			return false # does not match given pack
	
	# check if empty slots
	if not get_card_has_empty_decorator_slots():
		return false
	
	return true
#endregion

#region Card Flag Getters
## Returns if card exhausts when played.
func does_card_exhaust() -> bool:
	return card_play_destination == HandManager.EXHAUST_PILE
## Returns if card exhausts end of turn if in hand.
func is_card_ethereal() -> bool:
	return card_is_ethereal or card_end_of_turn_destination == HandManager.EXHAUST_PILE 
## Returns if card stays in hand end of turn. Does not factor temporary retain.
func does_card_retain() -> bool:
	return card_is_retained or card_end_of_turn_destination == HandManager.HAND_PILE
## If a card is banished when played
func does_card_banish() -> bool:
	return card_play_destination == HandManager.BANISH_PILE
#endregion
