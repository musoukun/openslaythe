## Validator for checking if the player has at least 1 card that can be decorated with at least one
## specified card decorator.
## NOTE: This is used for "enchanting cards" rest action. You may wish to change the logic for this
## to determine what is considered "enchantable".
extends BaseValidator

func _validation(_card_data: CardData, _action: BaseAction, values: Dictionary[String, Variant]) -> bool:
	var card_decorator_ids: Array[String] = []
	card_decorator_ids.assign(_get_validator_value("card_decorator_ids", values, _action, []))
	if len(card_decorator_ids) == 0:
		DebugLogger.log_error("ValidatorDeckHasDecoratableCard: No decorators provided")
	
	# at least one card must decoratable by at least one decorator
	for card: CardData in Global.player_data.player_deck:
		for card_decorator_id: String in card_decorator_ids:
			if card.is_card_decorator_applicable(card_decorator_id):
				return true
	return false
