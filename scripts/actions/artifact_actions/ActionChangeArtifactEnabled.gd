## Can enable or disable an artifact. Disabled artifacts can no longer perform actions or change their
## counters.
## Can target either an artifact type or a specific
## instance of an artifact/
## Typically used by single use artifacts to disable themselves.
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		var artifact_id: String = action_interceptor_processor.get_shadowed_action_values("artifact_id", "")
		var artifact_disabled: int = action_interceptor_processor.get_shadowed_action_values("artifact_disabled", true)
		
		# (dis)able artifacts of a specific id (technically allows duplicates)
		if artifact_id != "":
			var artifacts_with_artifact_id: Array[ArtifactData] = Global.player_data.get_player_artifacts_with_artifact_id(artifact_id)
			for artifact_data: ArtifactData in artifacts_with_artifact_id:
				artifact_data.set_artifact_disabled(artifact_disabled)
		
		# (dis)able a specific artifact.
		# This is usually passed in from the artifact itself automatically from the artifact's
		# event related actions, via BaseArtifact._perform_artifact_actions(), but other things may
		# pass it in as well
		var artifact_data: ArtifactData = action_interceptor_processor.get_shadowed_action_values("artifact_data", null)
		if artifact_data != null:
			artifact_data.set_artifact_disabled(artifact_disabled)

func is_instant_action() -> bool:
	return true

func _to_string():
	var artifact_disabled: bool = get_action_value("artifact_disabled", 0)
	return "Enable Artifact Action" + str(artifact_disabled)
