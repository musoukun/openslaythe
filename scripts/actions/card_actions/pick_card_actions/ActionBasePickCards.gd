## Abstract class for any action which requires selecting cards from either the hand, deck, or a pile.
## Extend to provide functionality.
## NOTE: See HandManager piles, or PICK_<X> constants defined below for what cards you can target.
## NOTE: See ActionPickCards subclass for instead deferring logic to child cardset actions, which
## provides much more flexibility and is more user friendly.
## NOTE: Upon perform_action(), this action will be passed to Hand, CardSelectionOverlay, or CardDraftSelectionOverlay UI
## elements and modified with the selected cards before calling perform_async_action()
## NOTE: Override perform_async_action() to actually perform the action once the cards are selected and passed into picked_cards
extends BaseAsyncAction
class_name ActionBasePickCards

## The final cards picked automatically or by the player. Child actions of this will typically use
## this value.
var picked_cards: Array[CardData] = []

# special pick types specific to this action. See: get_card_pick_type().
# NOTE: See HandManager piles for other places to pick cards from.
## Selecting cards in a draft format. This pick type only determines the UI to use and has flags
## that determine what cards are actually selectable.
## If pick_draft_cards flag == true, they can be supplied externally via draft_cards action value.
## If draft_from_card_pool flag == true, they are generated from get_drafted_cards().
## See get_input_cardset() for more.
const PICK_DRAFT: String = "PICK_DRAFT"
## Picking the card that initiated this action (found in the CardPlayRequest if one exists).
## Automatic selection, no UI used.
const PICK_PARENT_CARD: String = "PICK_PARENT_CARD"
## Pick cards next to the card that initiated this action. Only works for cards in hand, will silently fail otherwise.
## Automatic selection, no UI used.
const PICK_ADJACENT_CARDS: String = "PICK_ADJACENT_CARDS"

### Override These

func perform_async_action() -> void:
	# override this to provide functionality after the player or game has picked the cards
	# picked_cards will be populated at this point and you can manipulate them
	action_async_finished.emit()

## Gets the display message for the user when picking cards.
## Uses card_pick_text from card's values.
## Formatted string of {0} for max cards, {1} for cards picked, and {2} for cards remaining.
## override for messages requiring different formatting
func get_card_pick_text() -> String:
	var max_card_amount: int = get_card_pick_max_amount()
	var picked_card_amount: int = len(picked_cards)
	var remaining_card_amount: int = max_card_amount - picked_card_amount
	var pickable_cards_max_amount: int = get_pickable_cards_max_amount()
	
	var card_pick_text: String = get_action_value("card_pick_text", "Choose {0} card(s). {1} cards selected")
	var returned_text: String = card_pick_text.format([max_card_amount, picked_card_amount, remaining_card_amount, pickable_cards_max_amount])
	return returned_text

func _to_string():
	return "Base Card Pick Action"

### Keep

## Returns the source cards you pick from, before additional validators are applied.
## Supports all card pick types, as well as drafting cards.
## Defaults to getting the player hand.
func get_input_cardset() -> Array[CardData]:
	var card_pick_type: String = get_card_pick_type()
	
	# returns this card
	if card_pick_type == ActionBasePickCards.PICK_PARENT_CARD:
		if card_play_request != null:
			if card_play_request.card_data != null:
				return [card_play_request.card_data] as Array[CardData]
	# returns adjacent cards
	if card_pick_type == ActionBasePickCards.PICK_ADJACENT_CARDS:
		return _get_action_adjacent_cards()
	
	# can inject cards to select from via draft_cards
	# useful for RewardOverlay which pre-generates card rewards
	var pick_draft_cards: bool = get_action_value("pick_draft_cards", false)
	if pick_draft_cards:
		var draft_cards: Array[CardData] = []
		draft_cards.assign(get_action_value("draft_cards", []))
		if len(draft_cards) > 0:
			return draft_cards
		else:
			DebugLogger.log_error("No Provided Draft Cards")
			return draft_cards
	
	# can generate random cards to pick from
	# mainly useful for combat
	var draft_from_card_pool: bool = get_action_value("draft_from_card_pool", false)

	if draft_from_card_pool:
		return get_drafted_cards()
	
	return HandManager.get_pile(card_pick_type)

func perform_action():
	# determine if its possible to select the cards from the input card set
	# the number of min cards and min requirement determine if the action is performable
	# and if its automatically performed
	var pickable_cards: Array[CardData] = get_pickable_cards() # automatically obtain list of pickable cards from an input set
	
	# card selection params
	var min_cards_are_required: bool = get_min_cards_are_required_for_action()
	var random_selection: bool = get_action_value("random_selection", false) 	# to select the cards randomly without player input
	var min_card_amount: int = get_card_pick_min_amount()
	
	# parent card type automatically sets parameters
	var card_pick_type: String = get_card_pick_type()
	if card_pick_type == ActionBasePickCards.PICK_PARENT_CARD:
		random_selection = true
		min_card_amount = 1
		min_cards_are_required = false
	
	if len(pickable_cards) < min_card_amount:
		# not enough cards
		if min_cards_are_required:
			# not enough cards to perform the card action, do nothing
			await Global.get_tree().process_frame # add a delay to allow ActionHandler to catch up with async to avoid infinite hang
			action_async_finished.emit()
			return
		else:
			# automatically select the cards
			picked_cards = pickable_cards
			await Global.get_tree().process_frame # add a delay to allow ActionHandler to catch up with async to avoid infinite hang
			perform_async_action()
			return
	elif len(pickable_cards) == min_card_amount:
		# exactly enough cards; automatically select them
		picked_cards = pickable_cards
		await Global.get_tree().process_frame # add a delay to allow ActionHandler to catch up with async to avoid infinite hang
		perform_async_action()
		return
	else:
		# more than min cards
		if random_selection:
			# automatically randomly select the cards
			var rng_name: String = get_action_value("rng_name", "rng_card_picking")
			var rng_card_picking: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)
			
			# randomize card order and pick first X cards
			pickable_cards = Random.shuffle_array(rng_card_picking, pickable_cards)
			picked_cards = pickable_cards.slice(0, min_card_amount)

			await Global.get_tree().process_frame # add a delay to allow ActionHandler to catch up with async to avoid infinite hang
			perform_async_action()
			return
		else:
			# prompt the user for card input
			async_awaiting = true 
			Signals.card_pick_requested.emit(self)
			await Signals.card_pick_confirmed
			async_awaiting = false
			perform_async_action()
			return

func is_instant_action() -> bool:
	return true

### Card Picking

## Some support for drafting random cards, such as cards that generate random cards in
## combat that the player can then select.
## NOTE: This is typically not useful for general card rewards because generation happens at time of
## action and is not saved.
## Still useful for generating random cards in combat, or generating rewards through
## deterministic criteria (eg pick a rare card from all rare cards)
## You may use a predefined card pack, use the card pool available to the player,
## or filter all cards using validator criteria.
func get_drafted_cards() -> Array[CardData]:
	var filtered_card_draft: Array[CardData] = []
	
	# a specific card pack to use
	# for complex queries you may wish to generate a card pack specific for the draft rather
	# than narrowing from all cards with validators each time
	var draft_card_pack_id: String = get_action_value("draft_card_pack_id", "")
	
	# use the cards that the player is capable of drafting, from PlayerData
	var draft_use_player_draft: bool = get_action_value("draft_use_player_draft", false)
	
	# randomize ordering and reduce to a max number of cards
	var rng_name: String = get_action_value("rng_name", "rng_non_reward_card_drafting")
	var rng_non_reward_card_drafting: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)
	var draft_max_card_amount: int = get_action_value("draft_max_card_amount", 3) # 0 or negative for all cards. Use DECK card pick type for larger ui selections
	
	if draft_card_pack_id != "":
		#TODO support weighting for card pack based drafting
		filtered_card_draft = Random.generate_unweighted_card_draft_from_card_pack_id(rng_non_reward_card_drafting, draft_card_pack_id, draft_max_card_amount)
	elif draft_use_player_draft:
		# generate a draft from player available cards
		# can be weighted or unweighted
		# NOTE: validator_data should be empty for this kind of draft or it may break the
		# draft once it hits get_pickable_cards() and runs the validator over them
		var draft_probability_is_weighted: bool = get_action_value("draft_is_weighted", false)
		var draft_use_pity_system: bool = get_action_value("draft_use_pity_system", false)
		if draft_probability_is_weighted:
			filtered_card_draft = Random.generate_rarity_weighted_card_draft(rng_non_reward_card_drafting, draft_max_card_amount, Random.CARD_DRAFT_TABLE_TYPES.STANDARD, draft_use_pity_system)
		else:
			filtered_card_draft = Random.generate_unweighted_card_draft(rng_non_reward_card_drafting, draft_max_card_amount)	
	else:
		# generate a draft from all cards and narrow using validators
		var card_validator_data: Array = get_card_pick_validator_data()
		
		var card_ids: Array[String] = CardFilter.new().filter_card_validators(card_validator_data).convert_to_unique_card_object_ids()
		card_ids = Random.shuffle_slice_array(rng_non_reward_card_drafting, card_ids, draft_max_card_amount)
		# generate the card instances
		filtered_card_draft = Global.get_card_data_from_prototypes(card_ids)
	
	return filtered_card_draft

### Picking Validation Methods

## Validates if manual selection will automatically confirm when maximum number of cards are picked.
## Especially useful for when there's only 1 card.
func is_quick_pick() -> bool:
	var quick_pick: bool = get_action_value("quick_pick", true)
	if quick_pick:
		var picked_card_amount: int = len(picked_cards)
		return len(picked_cards) >= get_card_pick_max_amount()
	return false

func get_card_pick_type() -> String:
	return get_action_value("card_pick_type", HandManager.HAND_PILE)
	
func get_card_pick_validator_data() -> Array:
	# returns validators applied to any cards the user can pick
	return get_action_value("validator_data", [])

## The number of cards needed to be selected or the following actions will not be performed.
## If the number of selectable cards is below the min_card_amount, it will automatically fail.
func get_min_cards_are_required_for_action() -> int:
	return get_action_value("min_cards_are_required_for_action", false)

## The minimum number of cards required for this card pick.
## If min_cards_are_required_for_action = true, then actions will not fire if selection falls
## below this value.
func get_card_pick_min_amount() -> int:
	return get_action_value("min_card_amount", 0)

## The maximum number of cards required for this card pick to be considered valid.
func get_card_pick_max_amount() -> int:
	return get_action_value("max_card_amount", HandManager.PLAYER_DEFAULT_HAND_CARD_COUNT_MAX)

## Gets how many cards are available for selection after card filters are applied. Useful for things like
## getting first X cards from top of discard/draw pile
func get_pickable_cards_max_amount() -> int:
	return get_action_value("pickable_cards_max_amount", -1)

## If the back button will appear, allowing you to cancel the card picking selection.
## NOTE: This is ONLY useful for the CardSelectionOverlay used in deck related actions, particularly
## rest actions that involve card picking.
## NOTE: When the back button is used it will be considered a selection of 0 cards. This should usually
## be used with a min_card_amont of 1 or more, and min_cards_are_required_for_action = true. When using
## rest actions, should be combined with ActionConfirmRestAction child action and RestActionData.
func get_card_pick_can_back_out() -> bool:
	return get_action_value("can_back_out", false)


## Gets all cards that meet pickable criteria from a given input list of cards.
## This factors in additonal validators that can be supplied.
func get_pickable_cards() -> Array[CardData]:
	var input_cardset: Array[CardData] = get_input_cardset()
	var pickable_cards: Array[CardData] = []
	var parent_card: CardData = get_action_card_data()
	
	# filter out cards that fail validation
	pickable_cards = CardFilter.new(input_cardset).filter_card_validators(get_card_pick_validator_data()).filtered_cards
	
	var card_pick_type: String = get_card_pick_type()
	if card_pick_type == ActionBasePickCards.PICK_PARENT_CARD:
		# return the parent card
		return pickable_cards
	else:
		# ignore the card that generated this action
		pickable_cards.erase(parent_card)
	
	# limits the selection to the first N results. Eg: first 3 attack cards from draw pile
	# instead of showing all attack cards in draw pile
	var pickable_cards_max_amount: int = get_pickable_cards_max_amount()
	if pickable_cards_max_amount > 0 and len(pickable_cards) >= pickable_cards_max_amount:
		pickable_cards = pickable_cards.slice(0, pickable_cards_max_amount)
	
	return pickable_cards

func are_enough_cards_picked() -> bool:
	var min_card_amount: int = get_card_pick_min_amount()
	var max_card_amount: int = get_card_pick_max_amount()
	var picked_card_amount: int = len(picked_cards)
	return (min_card_amount <= picked_card_amount) and (picked_card_amount <= max_card_amount)  

## Optional Override.
## Method for determining if a given card can be selected for this action.
## For example limiting the player to only picking cards that are above an energy cost.
## check_max_pick_size = false can be used to check if the card simply meets pick criteria, though
## will usually be true.
func is_card_pickable(card_data: CardData, check_max_pick_size: bool = true) -> bool:
	var max_card_amount: int = min(get_card_pick_max_amount(), HandManager.PLAYER_DEFAULT_HAND_CARD_COUNT_MAX)
	if (len(picked_cards) >= max_card_amount) and check_max_pick_size:
		return false
	
	# run card through validators, should return either empty array or contain the card
	var card_validator_data: Array = get_card_pick_validator_data()
	var validated_card: Array[CardData] = CardFilter.new([card_data]).filter_card_validators(card_validator_data).filtered_cards
	if len(validated_card) == 0:
		return false
		
	return true	# by default all cards are pickable

## Forces the card pick to end
func force_action_end() -> void:
	if async_awaiting:
		picked_cards = []
		Signals.card_pick_confirmed.emit()
		async_awaiting = false

## Used for PICK_ADJACENT_CARDS card pick type. Only works for cards in hand.
func _get_action_adjacent_cards() -> Array[CardData]:
	var card_data: CardData = get_action_card_data()
	if card_data == null:
		DebugLogger.log_error("No CardData or CardPlayRequest found to determine adjacent cards")
		return []
	
	var hand_at_play_time: Array[CardData] = card_play_request.hand_at_play_time
	
	# position of the card in hand
	var card_index: int = hand_at_play_time.find(card_data)
	if card_index == -1:
		return [] # card not in hand, thus no cards adjacent
	
	var adjacent_cards: Array[CardData] = []
	
	# get left card
	if card_index > 0:
		var left_card_data: CardData = hand_at_play_time[card_index - 1]
		adjacent_cards.append(left_card_data)
	
	# get right card
	if card_index + 1 < len(hand_at_play_time):
		var right_card_data: CardData = hand_at_play_time[card_index + 1]
		adjacent_cards.append(right_card_data)
	
	return adjacent_cards
