## This action will modify the destination of the *current* card play's destination.
## This is useful for cards with condition effects when combined with ActionValidator, ex
## a card that can be played 3 times before exhausting.
## WARNING: This does not actually mutate the card and will have no effect outside of a singular card play.
## See ActionChangeCardProperties targeting card_play_destination to do that.
extends BaseAction

func is_instant_action() -> bool:
	return true

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([null])
	
	for action_interceptor_processor in action_interceptor_processors:
		if card_play_request == null:
			DebugLogger.log_error("No CardPlayRequest found to change play destination")
			breakpoint
			return
		# changes the pile and strategy
		var card_destination_pile: String = action_interceptor_processor.get_shadowed_action_values("card_destination", card_play_request.card_destination_pile)
		var card_destination_strategy: int = action_interceptor_processor.get_shadowed_action_values("card_destination_strategy", card_play_request.card_destination_strategy)
		card_play_request.card_destination_pile = card_destination_pile
		card_play_request.card_destination_strategy = card_destination_strategy
		
