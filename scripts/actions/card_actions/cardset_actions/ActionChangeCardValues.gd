## Updates cards' values to a new version. Only changes provided key-values for the card.
## See ActionChangeCardProperties for affecting CardData properties and not CardData.card_values
extends BaseCardsetAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([null])
	for action_interceptor_processor in action_interceptor_processors:
		var modify_parent_card: bool = action_interceptor_processor.get_shadowed_action_values("modify_parent_card", true)
		var picked_cards: Array[CardData] = _get_picked_cards()
		
		# iterate over the cards, improving them and/or their parent
		for card_data in picked_cards:
			# get parent card if improving that
			var parent_card_data: CardData = null
			if modify_parent_card:
				if card_data.parent_card == null:
					DebugLogger.log_error("No parent card found. Are you modifying the permanent version?")
				else:
					parent_card_data = card_data.parent_card
			
			# iterate over the card's values, adding to them where necessary
			var new_card_values: Dictionary[String, Variant] = {}
			new_card_values.assign(action_interceptor_processor.get_shadowed_action_values("new_card_values", {})) # assign to force typed dict
			
			if card_data != null:
				card_data.update_card_values(new_card_values)
			if parent_card_data != null:
				parent_card_data.update_card_values(new_card_values)

func _to_string():
	return "Improve Card Action"
