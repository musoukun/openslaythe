## General use cardset action that changes CardData properties (not card_values!)
## to a given value using set(), overwriting existing values.
## This can target a list of cards, or their parent cards (making it permanent if in player's deck)
## See ActionImproveCardValues for additive version that affects card_values
extends BaseCardsetAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
	
		var modify_parent_card: bool = action_interceptor_processor.get_shadowed_action_values("modify_parent_card", true)
		var picked_cards: Array[CardData] = _get_picked_cards()
		
		for card_data in picked_cards:
			# get parent card if changing that that
			var parent_card_data: CardData = null
			if modify_parent_card:
				if card_data.parent_card == null:
					DebugLogger.log_error("No parent card found. Are you modifying the permanent version?")
				else:
					parent_card_data = card_data.parent_card

			var card_properties: Dictionary[String, Variant] = {}
			card_properties.assign(action_interceptor_processor.get_shadowed_action_values("card_properties", {})) # assign to force typed dict
		
			if card_data != null:
				card_data.set_card_properties(card_properties)
			if parent_card_data != null:
				parent_card_data.set_card_properties(card_properties)

func _to_string():
	return "Change Card Properties Action"
