## Applies decorators to cards. See CardDecoratorData.
## This can target a list of cards, or their parent cards (making it permanent if in player's deck)
extends BaseCardsetAction

func perform_action():
	var picked_cards: Array[CardData] = _get_picked_cards()
	
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		# whether to decorate the combat card or permanent card
		var decorate_parent_card: bool = action_interceptor_processor.get_shadowed_action_values("decorate_parent_card", true)
		
		# defines a specifc id and values. If empty random selection will be used
		var card_decorator_object_id: String = action_interceptor_processor.get_shadowed_action_values("card_decorator_object_id", "")
		var card_decorator_values: Dictionary[String, Variant] = {}
		card_decorator_values.assign(action_interceptor_processor.get_shadowed_action_values("card_decorator_values", {}))
		
		# randomly selects between these decorators. {decorator_id: decorator_values}
		# will attempt to use a decorator that is not already applied
		var random_card_decorators: Dictionary[String, Dictionary] = {}
		random_card_decorators.assign(action_interceptor_processor.get_shadowed_action_values("random_card_decorators", {}))
		var rng_name: String = action_interceptor_processor.get_shadowed_action_values("rng_name", "rng_card_decoration") # allows using different rng
		var rng_card_decoration: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)
		
		# iterate over the cards, decorating them and/or their parent
		for card_data: CardData in picked_cards:
			var selected_card_decorator_object_id: String = card_decorator_object_id
			var selected_card_decorator_values: Dictionary[String, Variant] = card_decorator_values
			# use a random selection if no id defined
			if selected_card_decorator_object_id == "":
				if len(random_card_decorators) == 0:
					DebugLogger.log_error("ActionDecorateCards: No decorator id or ids defined")
					breakpoint
					return
				else:
					# randomize the ordered list of decorators and accept the first one that is viable
					var randomized_decorator_ids: Array[String] = random_card_decorators.keys()
					randomized_decorator_ids = Random.shuffle_array(rng_card_decoration, randomized_decorator_ids)
					for random_decorator_id: String in randomized_decorator_ids:
						if card_data.is_card_decorator_applicable(random_decorator_id):
							selected_card_decorator_object_id = random_decorator_id
							selected_card_decorator_values.assign(random_card_decorators[selected_card_decorator_object_id])
							break # stop random search
					
					# cannot decorate this card as no decorator provided and random selection failed
					if selected_card_decorator_object_id == "":
						DebugLogger.log_warning("ActionDecorateCards: no empty slots for random selection")
						continue # go to the next card
			
			# get parent card if decorating that
			var parent_card_data: CardData = null
			if decorate_parent_card:
				if card_data.parent_card == null:
					DebugLogger.log_error("No parent card found")
					return
				else:
					parent_card_data = card_data.parent_card
			
			# decorate the card
			if card_data != null:
				card_data.add_card_decorator(selected_card_decorator_object_id, card_decorator_values)
			# decorate parent
			if parent_card_data != null:
				parent_card_data.add_card_decorator(selected_card_decorator_object_id, card_decorator_values)

func is_instant_action() -> bool:
	return true

func _to_string():
	return "Decorate Card Action"
