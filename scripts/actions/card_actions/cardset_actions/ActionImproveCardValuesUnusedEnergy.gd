## A specific version of ActionImproveCardValues that factors in the player's energy as a multiplier
extends BaseCardsetAction

func perform_action():
	var picked_cards: Array[CardData] = _get_picked_cards()
	
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		var modify_parent_card: bool = action_interceptor_processor.get_shadowed_action_values("modify_parent_card", true)
		# get the player's energy and use it to multiply any numbers
		var card_value_improvements: Dictionary[String, int] = {}
		card_value_improvements.assign(action_interceptor_processor.get_shadowed_action_values("card_value_improvements", {})) # assign to force typed dict
		
		var player_energy: int = Global.player_data.player_energy
		for key_name: String in card_value_improvements.keys():
			var improvement_value: Variant = card_value_improvements[key_name]
			if improvement_value is int or improvement_value is float:
				improvement_value *= player_energy
				card_value_improvements[key_name] = improvement_value
	
		# iterate over the cards, improving them and/or their parent
		for card_data in picked_cards:
			# get parent card if improving that
			var parent_card_data: CardData = null
			if modify_parent_card:
				if card_data.parent_card == null:
					DebugLogger.log_error("ActionImproveCardValuesUnusedEnergy: No parent card found")
				else:
					parent_card_data = card_data.parent_card
			
			if card_data != null:
				card_data.improve_card_values(card_value_improvements)
			if parent_card_data != null:
				parent_card_data.improve_card_values(card_value_improvements)

func _to_string():
	return "Improve Card Action"
