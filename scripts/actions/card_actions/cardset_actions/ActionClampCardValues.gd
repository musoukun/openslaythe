## Clamps a given cardset's card_values to a given range
## This can target a list of cards, or their parent cards (making it permanent if in player's deck)
extends BaseCardsetAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action()
	
	for action_interceptor_processor in action_interceptor_processors:
		var modify_parent_card: bool = action_interceptor_processor.get_shadowed_action_values("modify_parent_card", true)
		var picked_cards: Array[CardData] = _get_picked_cards()
		
		# iterate over the cards, improving them and/or their parent
		for card_data in picked_cards:
			# get parent card if improving that
			var parent_card_data: CardData = null
			if modify_parent_card:
				if card_data.parent_card == null:
					DebugLogger.log_error("ActionClampCardValues: No parent card found")
				else:
					parent_card_data = card_data.parent_card
			
			# iterate over the card's values, adding to them where necessary
			var card_value_improvements: Dictionary[String, Array] = {}
			card_value_improvements.assign(action_interceptor_processor.get_shadowed_action_values("clamped_card_values", {})) # assign to force typed dict
			
			if card_data != null:
				card_data.clamp_card_values(card_value_improvements)
			if parent_card_data != null:
				parent_card_data.clamp_card_values(card_value_improvements)

func _to_string():
	return "Clamp Card Action"
