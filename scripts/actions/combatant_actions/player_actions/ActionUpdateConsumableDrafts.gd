## Changes the types of consumables available to the player for future consumable rewards.
## Forces a recompiling of PlayerData.player_reward_consumable_filter_cache
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor: ActionInterceptorProcessor in action_interceptor_processors:
		# option to reset to character's starting consumable packs
		var reset_to_starting_consumable_packs: bool = action_interceptor_processor.get_shadowed_action_values("reset_to_starting_consumable_packs", false)
		if reset_to_starting_consumable_packs:
			Global.player_data.reward_draft_consumable_pack_ids = []
			var character_data: CharacterData = Global.get_character_data(Global.player_data.player_character_object_id)
			Global.player_data.reward_draft_consumable_pack_ids.assign(character_data.character_starting_consumable_pack_ids)
		
		# option to reset to character's starting consumable packs
		var remove_all_consumable_packs: bool = action_interceptor_processor.get_shadowed_action_values("remove_all_consumable_packs", false)
		if remove_all_consumable_packs:
			Global.player_data.reward_draft_consumable_pack_ids = []
		
		# adding consumable packs
		var add_consumable_pack_object_ids: Array[String] = []
		add_consumable_pack_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("add_consumable_pack_object_ids", []))
		
		for consumable_pack_object_id: String in add_consumable_pack_object_ids:
			if not Global.player_data.reward_draft_consumable_pack_ids.has(consumable_pack_object_id):
				Global.player_data.reward_draft_consumable_pack_ids.append(consumable_pack_object_id)
		
		# removing consumable packs
		var remove_consumable_pack_object_ids: Array[String] = []
		remove_consumable_pack_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("remove_consumable_pack_object_ids", []))
		
		for consumable_pack_object_id: String in remove_consumable_pack_object_ids:
			Global.player_data.reward_draft_consumable_pack_ids.erase(consumable_pack_object_id)
		
		# whitelist consumable ids
		var whitelist_consumable_object_ids: Array[String] = []
		whitelist_consumable_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("whitelist_consumable_object_ids", []))
		
		for whitelist_consumable_object_id: String in whitelist_consumable_object_ids:
			if not Global.player_data.player_reward_draft_consumable_id_whitelist.has(whitelist_consumable_object_id):
				Global.player_data.player_reward_draft_consumable_id_whitelist.append(whitelist_consumable_object_id)
			# remove blacklisted consumables if whitelisted
			if Global.player_data.player_event_blacklisted_ids.has(whitelist_consumable_object_id):
				Global.player_data.player_event_blacklisted_ids.erase(whitelist_consumable_object_id)
		
		# blacklist consumable ids
		var blacklist_consumable_object_ids: Array[String] = []
		blacklist_consumable_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("blacklist_consumable_object_ids", []))
		
		for blacklist_consumable_object_id: String in blacklist_consumable_object_ids:
			if not Global.player_data.player_reward_draft_consumable_id_blacklist.has(blacklist_consumable_object_id):
				Global.player_data.player_reward_draft_consumable_id_blacklist.append(blacklist_consumable_object_id)
			# remove whitelisted consumables if blacklisted
			if Global.player_data.player_reward_draft_consumable_id_whitelist.has(blacklist_consumable_object_id):
				Global.player_data.player_reward_draft_consumable_id_whitelist.erase(blacklist_consumable_object_id)
		
		
		# apply update to player drafting
		Global.player_data.regenerate_consumable_available_id_cache()
