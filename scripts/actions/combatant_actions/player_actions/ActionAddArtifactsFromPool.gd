## Given search criteria, adds the next artifact(s) from the player's available artifact pool
## to the player.
## Eg, add 2 common artifacts to the player.
extends BaseAction

func is_instant_action() -> bool:
	return true

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		
		var artifact_count: int = action_interceptor_processor.get_shadowed_action_values("artifact_count", 1)
		
		var artifact_rarities: Array[int] = []
		artifact_rarities.assign(action_interceptor_processor.get_shadowed_action_values("artifact_rarities", []))
		
		# if true will try to get artifacts with given rarities in order of rarities presented
		var use_rarity_ordering: bool = action_interceptor_processor.get_shadowed_action_values("use_rarity_ordering", true)
		# will try and get artifacts in reverse order in pool
		var from_back: bool = action_interceptor_processor.get_shadowed_action_values("from_back", false)
		
		# get the next available artifacts to add to the player
		var artifact_ids: Array[String] = Global.player_data.get_next_artifacts_from_pool(artifact_count, artifact_rarities, use_rarity_ordering, from_back, true)
		
		for artifact_id: String in artifact_ids:
			var artifact_data: ArtifactData = Global.get_artifact_data(artifact_id)
			if artifact_data == null:
				breakpoint
				DebugLogger.log_error("ActionAddArtifact: No artifact with ID of \"{0}\"".format([artifact_id]))
				return
			
			# generates an interceptable action and performs it to add a player artifact
			ActionGenerator.generate_add_artifact(artifact_id)

func _to_string():
	var artifact_id: String = get_action_value("artifact_id", "")
	return "Add Artifact Action: " + artifact_id
