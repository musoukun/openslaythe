## Interceptable action to use a consumable in a given slot
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		var consumable_slot_index: int = action_interceptor_processor.get_shadowed_action_values("consumable_slot_index", 0)
		# this flag will force all actions to be processed instantly rather than added to the stack. This has niche use for auto consumables
		# especially auto-revives which must happen instantly between attacks.
		var perform_comsumable_actions_instantly: bool = action_interceptor_processor.get_shadowed_action_values("perform_comsumable_actions_instantly", false)
		
		# ensure consumable exists in slot
		var consumable_data: ConsumableData = Global.get_player_consumable_in_slot_index(consumable_slot_index)
		if consumable_data == null:
			DebugLogger.log_error("ActionUseComsumable: No consumable found in slot {0}".format([consumable_slot_index]))
			breakpoint
			return
		else:
			# remove consumable
			var player_data: PlayerData = Global.player_data
			player_data.player_consumable_slot_to_consumable_object_id.erase(str(consumable_slot_index))
			
			# perform actions of consumable
			if consumable_data != null:
				var action_data: Array[Dictionary] = consumable_data.consumable_actions
				var player: Player = Global.get_player()
				# generate a fake card play request with duplicated consumable values
				var _card_play_request: CardPlayRequest = HandManager.create_card_play_request(null, null, false, true)
				var consumable_values: Dictionary = consumable_data.consumable_values.duplicate()
				_card_play_request.card_values = consumable_values
				
				var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(player, _card_play_request, targets, action_data, null)
				
				# decide whether to perform instantly or add to stack
				if perform_comsumable_actions_instantly:
					# instant
					for action: BaseAction in generated_actions:
						action.perform_action()
				else:
					# stack
					ActionHandler.add_actions(generated_actions)
			
			Signals.consumable_used.emit(consumable_slot_index, consumable_data.object_id)
		

func _to_string():
	var consumable_slot_index: int = get_action_value("consumable_slot_index", "")
	return "Use Consumable Action: " + str(consumable_slot_index)
