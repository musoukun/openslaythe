## Converts block to a given status effect
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action()
	
	for action_interceptor_processor: ActionInterceptorProcessor in action_interceptor_processors:
		var target: BaseCombatant = action_interceptor_processor.target
		if target == null:
			continue
		
		var status_effect_object_id: String = action_interceptor_processor.get_shadowed_action_values("status_effect_object_id", "")
		if status_effect_object_id == "":
			DebugLogger.log_error("No status effect id provided")
			breakpoint
			continue
		
		# create instant status action using block for charges
		var action_data: Array[Dictionary] = [
			{Scripts.ACTION_APPLY_STATUS: {
				"status_effect_object_id": status_effect_object_id,
				"status_charge_amount": target.get_block()
			}}
		]
		
		# perform action instantly
		var generated_action: BaseAction = ActionGenerator.create_actions(parent_combatant, card_play_request, [target], action_data, self)[0]
		generated_action.perform_action()
		
		# remove block
		target.reset_block()

func _to_string():
	return "Block To Status Action"
