## Meta action that performs for-loop equivalent, generating an action payload a given number of times.
## NOTE: For drawing cards or generating attacks, it is recommended to use the more specific
## ActionDrawGenerator and ActionAttackGenerator as they are intended for interception while
## this one is not.
extends BaseAction

func is_instant_action() -> bool:
	return true

func perform_action(): 
	var action_data: Array[Dictionary] = []
	action_data.assign(get_action_value("action_data", []))
	
	# number of times to repeat
	var action_count: int = get_action_value("action_count", 1)
	var repeated_action_data: Array[Dictionary] = []
	for _i: int in max(0, action_count):
		repeated_action_data.append_array(action_data)

	var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(parent_combatant, card_play_request, targets, repeated_action_data, self)
	ActionHandler.add_actions(generated_actions)

func _to_string():
	return "Action Generate Actions"
