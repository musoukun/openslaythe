## Uses a random weighting to determine what child action payloads to use
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([null])
	for action_interceptor_processor in action_interceptor_processors:
		# Weights should be {"action_payload_name_1": weight, ...}
		var weights: Dictionary[Variant, int] = {}
		weights.assign(action_interceptor_processor.get_shadowed_action_values("weights", {}))
		
		# Actions should be {"action_payload_name_1": action_data, ...}
		var weighted_action_data: Dictionary[String, Array] = {}
		weighted_action_data.assign(action_interceptor_processor.get_shadowed_action_values("weighted_action_data", {}))
		
		var rng_name: String = action_interceptor_processor.get_shadowed_action_values("rng_name", "rng_general")
		var rng_general: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)
		
		var selection: String = Random.get_weighted_selection(rng_general, weights)
		var action_data: Array[Dictionary] = []
		action_data.assign(weighted_action_data.get(selection, {}))
	
		var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(parent_combatant, card_play_request, targets, action_data, self)
		ActionHandler.add_actions(generated_actions)

func _validate() -> bool:
	# checks if action passes all validators
	var validators: Array[Dictionary] = []
	validators.assign(get_action_value("validator_data", []))
	return Global.validate(validators, card_play_request.card_data, self)

func is_instant_action() -> bool:
	return true
