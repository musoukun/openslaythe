## Modifies the number of cards drawn at the start of the turn. Does not modify all draw.
extends BaseActionInterceptor

const INCREASE_TURN_DRAW_STATUS_EFFECT_ID: String = "status_effect_increase_turn_draw"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, _preview_mode: bool = false) -> int:
	if _preview_mode:
		return ACTION_ACCEPTENCES.CONTINUE
	
	var parent_combatant: BaseCombatant = action_interceptor_processor.parent_action.parent_combatant
	if parent_combatant == null:
		return ACTION_ACCEPTENCES.REJECTED
	if not parent_combatant.is_alive():
		return ACTION_ACCEPTENCES.REJECTED
	
	var is_start_of_turn_draw: bool = action_interceptor_processor.get_shadowed_action_values("is_start_of_turn_draw", false)
	if not is_start_of_turn_draw:
		return ACTION_ACCEPTENCES.CONTINUE
	
	var draw_increase_charges: int = parent_combatant.get_status_charges(INCREASE_TURN_DRAW_STATUS_EFFECT_ID)
	var draw_count: int = action_interceptor_processor.get_shadowed_action_values("draw_count", 0)
	var modified_draw_count: int = draw_count + draw_increase_charges
	action_interceptor_processor.set_shadowed_action_values("draw_count", modified_draw_count)
	
	# decay the status instantly
	ActionGenerator.generate_decay_status_effect(parent_combatant, INCREASE_TURN_DRAW_STATUS_EFFECT_ID, -draw_increase_charges)
	
	return ACTION_ACCEPTENCES.CONTINUE
