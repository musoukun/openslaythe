## The first X number of any non-duplicated standard discarding card plays are returned to the top of hand.
## This intercepts the special immediate action ActionCardPlay as it is being processed in Hand
extends BaseActionInterceptor

## The corresponding status for this interceptor. Will be decremented when it takes effect.
var REBOUND_CARD_PLAYS_STATUS_EFFECT_ID: String = "status_effect_rebound_card_plays"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, preview_mode: bool = false) -> int:
	if preview_mode:
		return ACTION_ACCEPTENCES.CONTINUE
	
	var parent_combatant: BaseCombatant = action_interceptor_processor.parent_action.parent_combatant
	
	var card_play_request: CardPlayRequest = action_interceptor_processor.parent_action.card_play_request
	# will not rebound duplicated plays
	if card_play_request.is_duplicate_play:
		return ACTION_ACCEPTENCES.CONTINUE
	# will not rebound non-discard plays
	if card_play_request.card_destination_pile != HandManager.DISCARD_PILE:
		return ACTION_ACCEPTENCES.CONTINUE
	
	# must have enough charges. This should always be the case just making sure
	var status_effects: Array[StatusEffect] = parent_combatant.status_id_to_status_effects.get(REBOUND_CARD_PLAYS_STATUS_EFFECT_ID, [])
	if len(status_effects) <= 0:
		breakpoint
		return ACTION_ACCEPTENCES.CONTINUE 
	
	# remove a charge
	var status_effect: StatusEffect = status_effects[0]
	parent_combatant.add_status_effect_charges(REBOUND_CARD_PLAYS_STATUS_EFFECT_ID, -1, 0)
	# change the card's destination to the top of the draw pile
	DebugLogger.log_line("Rebounding: " + action_interceptor_processor.parent_action.card_play_request.card_data.card_name)
	card_play_request.card_destination_pile = HandManager.DRAW_PILE
	card_play_request.card_destination_strategy = HandManager.PILE_INSERTION_STRATEGIES.TOP
	
	return ACTION_ACCEPTENCES.CONTINUE
