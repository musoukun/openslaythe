# Reduces damage by overshield amount
extends BaseActionInterceptor

const OVERSHIELD_STATUS_EFFECT_ID: String = "status_effect_overshield"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, preview_mode: bool = false) -> int:
	if preview_mode:
		return ACTION_ACCEPTENCES.CONTINUE	# don't negate damage in preview mode
	
	var target_combatant: BaseCombatant = action_interceptor_processor.target
	if target_combatant == null:
		return ACTION_ACCEPTENCES.REJECTED
	if not target_combatant.is_alive():
		return ACTION_ACCEPTENCES.REJECTED
	
	var damage: int = action_interceptor_processor.get_shadowed_action_values("damage", 0)
	var bypass_block: bool = action_interceptor_processor.get_shadowed_action_values("bypass_block", false)
	var target_block: int = target_combatant.get_block()
	if bypass_block:
		target_block = 0
	
	if damage > target_block:
		# reduce the status by remaining damage
		var remaining_damage: int = damage - target_block
		var overshield_charges: int = target_combatant.get_status_charges(OVERSHIELD_STATUS_EFFECT_ID)
		if overshield_charges > remaining_damage:
			action_interceptor_processor.set_shadowed_action_values("damage", 0)
			target_combatant.add_status_effect_charges(OVERSHIELD_STATUS_EFFECT_ID, -remaining_damage)
		else:
			target_combatant.add_status_effect_charges(OVERSHIELD_STATUS_EFFECT_ID, -overshield_charges)
			remaining_damage -= overshield_charges
			action_interceptor_processor.set_shadowed_action_values("damage", remaining_damage)
	
	return ACTION_ACCEPTENCES.CONTINUE
