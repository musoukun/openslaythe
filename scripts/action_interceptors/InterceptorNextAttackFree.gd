## Makes the cost of the next attack free by intercepting energy cost of CardPlay
## This will skip 0 and variable cost cards
extends BaseActionInterceptor

const NEXT_ATTACK_FREE_STATUS_EFFECT_ID: String = "status_effect_next_attack_free"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, preview_mode: bool = false) -> int:
	var parent_combatant: BaseCombatant = action_interceptor_processor.parent_action.parent_combatant
	var card_play_request: CardPlayRequest = action_interceptor_processor.parent_action.card_play_request
	
	if card_play_request.card_data == null:
		DebugLogger.log_error("No card provided for this interception. This should never happen.")
		breakpoint
		return ACTION_ACCEPTENCES.CONTINUE
	
	# only affects attack cards
	if card_play_request.card_data.card_type != CardData.CARD_TYPES.ATTACK:
		return ACTION_ACCEPTENCES.CONTINUE
	
	# must have sufficient status charges (should already be the case but ensure it)
	var status_charges: int = parent_combatant.get_status_charges(NEXT_ATTACK_FREE_STATUS_EFFECT_ID)
	if status_charges <= 0:
		return ACTION_ACCEPTENCES.CONTINUE
	
	# get cost
	var card_energy_cost: int = action_interceptor_processor.get_shadowed_action_values("card_energy_cost", card_play_request.card_data.get_card_energy_cost(true, true))
	
	# set the energy cost of the attack card to zero
	action_interceptor_processor.set_shadowed_action_values("card_energy_cost", 0)
	
	# cost already 0
	if card_energy_cost <= 0:
		return ACTION_ACCEPTENCES.CONTINUE
	
	# preview mode doesn't induce side effects
	if preview_mode:
		return ACTION_ACCEPTENCES.CONTINUE
	
	# duplicate plays don't remove status charges
	if card_play_request.is_duplicate_play:
		return ACTION_ACCEPTENCES.CONTINUE
	
	# remove a charge
	parent_combatant.add_status_effect_charges(NEXT_ATTACK_FREE_STATUS_EFFECT_ID, -1, 0)
	
	return ACTION_ACCEPTENCES.CONTINUE
