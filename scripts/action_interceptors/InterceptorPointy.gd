# Damages attackers
extends BaseActionInterceptor

const POINTY_STATUS_EFFECT_ID: String = "status_effect_pointy"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, preview_mode: bool = false) -> int:
	if preview_mode:
		return ACTION_ACCEPTENCES.CONTINUE
	
	var parent_combatant: BaseCombatant = action_interceptor_processor.parent_action.parent_combatant
	var target_combatant: BaseCombatant = action_interceptor_processor.target
	if parent_combatant == null or target_combatant == null:
		return ACTION_ACCEPTENCES.CONTINUE
	if not parent_combatant.is_alive() or not target_combatant.is_alive():
		return ACTION_ACCEPTENCES.CONTINUE
	
	# create damage action and add to front of action queue
	var pointy_charges: int = target_combatant.get_status_charges(POINTY_STATUS_EFFECT_ID)
	var action_data: Array[Dictionary] = [{
		Scripts.ACTION_DIRECT_DAMAGE: {
			"bypass_block": false,
			"damage": pointy_charges,
		}
	}]
	
	var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(target_combatant, null, [parent_combatant], action_data, null)
	ActionHandler.add_actions(generated_actions, true, true)
	
	return ACTION_ACCEPTENCES.CONTINUE
