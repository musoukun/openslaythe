## Used by auto revive consumables to heal the player and consume one of the auto revives.
## Requires the player to actually have the consumable to work.
extends BaseActionInterceptor

const AUTO_REVIVE_CONSUMAMBLE_ID: String = "consumable_auto_revive"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, preview_mode: bool = false) -> int:
	var parent_combatant: BaseCombatant = action_interceptor_processor.parent_action.parent_combatant # should be the player, but generic implementation

	# try to find an auto revive consumable in player's inventory
	var auto_revive_consumable_slot_index: int = -1
	for consumable_slot_index: int in Global.player_data.player_consumable_slot_count:
		var consumable_data: ConsumableData = Global.get_player_consumable_in_slot_index(consumable_slot_index)
		if consumable_data != null:
			if consumable_data.object_id == AUTO_REVIVE_CONSUMAMBLE_ID:
				auto_revive_consumable_slot_index = consumable_slot_index
				break
	
	# use the consumable, which should heal the player
	# this is processed instantly to ensure it happens before health is checked.
	if auto_revive_consumable_slot_index != -1:
		ActionGenerator.generate_use_consumable(parent_combatant, auto_revive_consumable_slot_index, true)
		return ACTION_ACCEPTENCES.STOPPED

	return ACTION_ACCEPTENCES.CONTINUE
