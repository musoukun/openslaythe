# Reduces incoming damage to a maximum threshold based on secondary status charges
extends BaseActionInterceptor

const CAP_DAMAGE_STATUS_EFFECT_ID: String = "status_effect_cap_damage"

func process_action_interception(action_interceptor_processor: ActionInterceptorProcessor, preview_mode: bool = false) -> int:
	if preview_mode:
		return ACTION_ACCEPTENCES.CONTINUE
	
	var target_combatant: BaseCombatant = action_interceptor_processor.target
	if target_combatant == null:
		return ACTION_ACCEPTENCES.REJECTED
	if not target_combatant.is_alive():
		return ACTION_ACCEPTENCES.REJECTED
	
	var damage: int = action_interceptor_processor.get_shadowed_action_values("damage", 0)
	var bypass_block: bool = action_interceptor_processor.get_shadowed_action_values("bypass_block", false)
	var target_block: int = target_combatant.get_block()
	if bypass_block:
		target_block = 0
	
	if damage > target_block:
		# cap incoming remaining damage by threshold amount 
		var remaining_damage: int = damage - target_block
		var cap_damage_secondary_charges: int = target_combatant.get_status_secondary_charges(CAP_DAMAGE_STATUS_EFFECT_ID)
		
		remaining_damage = min(remaining_damage, cap_damage_secondary_charges)
		action_interceptor_processor.set_shadowed_action_values("damage", remaining_damage)
	
	return ACTION_ACCEPTENCES.CONTINUE
