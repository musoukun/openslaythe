## A speech bubble component attached to a combatant
extends ColorRect
class_name SpeechBubble


@onready var timer: Timer = $Timer
@onready var auto_resize_rich_text_label: RichLabelAutoSizer = $AutoResizeRichTextLabel

## A list of unique messages to display
var queued_messages: Array[String] = []
## Rich text label doesn't store bbcode, so store it here to check for duplicates.
var current_bbcode: String = NO_MESSAGE

const NO_MESSAGE: String = ""

## Time in seconds that each message will display before hiding or moving
## to the next message.
const MESSAGE_TIME: float = 1.2

func _ready() -> void:
	timer.timeout.connect(_on_timeout)

func queue_message(message_bbcode: String) -> void:
	if queued_messages.has(message_bbcode) or current_bbcode == message_bbcode:
		return # don't display duplicates
	
	if len(queued_messages) == 0 and current_bbcode == NO_MESSAGE:
		current_bbcode = message_bbcode
		auto_resize_rich_text_label.set_bbcode(current_bbcode)
		visible = true
		timer.start(MESSAGE_TIME)
	else:
		queued_messages.append(message_bbcode)

func _on_timeout() -> void:
	if len(queued_messages) >= 1:
		var next_message_bbcode: String = queued_messages.pop_front()
		current_bbcode = next_message_bbcode
		auto_resize_rich_text_label.set_bbcode(current_bbcode)
		timer.start(MESSAGE_TIME)
	else:
		visible = false
		current_bbcode = NO_MESSAGE
		
