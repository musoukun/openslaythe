## A simple animated sprite that plays over combatants then frees itself.
## Primarily used for impacts during combat
## See ActionAttackGenerator and ActionCreateEffectAnimation
extends AnimatedSprite2D
class_name AnimatedCombatEffect

## Every combat effect animation should have a track with this name
const COMBAT_EFFECT_ANIMATION_NAME: String = AnimationData.ANIMATION_VFX

func _ready() -> void:
	animation_finished.connect(_on_animation_finished)

func init(animation_data: AnimationData) -> void:
	if animation_data == null:
		DebugLogger.log_error("AnimatedCombatEffect.init(): animation \"{0}\" not defined".format([animation_data.object_id]))
		queue_free()
		return
	
	var random_offset: Vector2 = Vector2i(
		randi_range(-animation_data.animation_vfx_offset_random, animation_data.animation_vfx_offset_random),
		randi_range(-animation_data.animation_vfx_offset_random, animation_data.animation_vfx_offset_random)
	)
	offset = animation_data.animation_offset + random_offset
	
	sprite_frames = animation_data.animations
	if sprite_frames.has_animation(COMBAT_EFFECT_ANIMATION_NAME):
		play(COMBAT_EFFECT_ANIMATION_NAME)
	else:
		DebugLogger.log_error("AnimatedCombatEffect.init(): animation \"{0}\" missing \"{1}\"".format([animation_data.object_id, COMBAT_EFFECT_ANIMATION_NAME]))
	

func _on_animation_finished():
	queue_free()
