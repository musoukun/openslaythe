## Displays consumables tab in the codex
extends BaseMenu

@onready var codex_consumable_rgsc: ResizingGridScrollContainer = %CodexConsumableRGSC

var sort_by_consumable_rarity: bool = true # if consumables should be sorted by rarity
var subsort_by_consumable_name_ascending: bool = true

func populate_menu() -> void:
	super()
	_populate_codex_consumables()
	codex_consumable_rgsc.call_deferred("resize_grid_columns")

func clear_menu() -> void:
	super()
	codex_consumable_rgsc.clear_children()

# creates display consumables in codex
func _populate_codex_consumables() -> void:
	var consumable_args: Array[Array] = [] # used to instantiate consumables in container
	var consumable_object_ids: Array = Global._id_to_consumable_data.keys()

	# generate data to make consumables
	for consumable_object_id: String in consumable_object_ids:
		var consumable_data: ConsumableData = Global.get_consumable_data(consumable_object_id)
		consumable_args.append([consumable_data])
	
	if len(consumable_args) > 1:
		consumable_args.sort_custom(_codex_consumable_custom_sort)
	
	# populate consumables
	codex_consumable_rgsc.populate_children(Scenes.CODEX_CONSUMABLE, consumable_args)

func _codex_consumable_custom_sort(consumable_args_1: Array, consumable_args_2: Array) -> bool:
	var consumable_data_1: ConsumableData = consumable_args_1[0]
	var consumable_data_2: ConsumableData = consumable_args_2[0]
	if sort_by_consumable_rarity:
		if consumable_data_1.consumable_rarity == consumable_data_2.consumable_rarity:
			return (consumable_data_1.consumable_name < consumable_data_2.consumable_name) == subsort_by_consumable_name_ascending
		return consumable_data_1.consumable_rarity < consumable_data_2.consumable_rarity
	else:
		return (consumable_data_1.consumable_name < consumable_data_2.consumable_name) == subsort_by_consumable_name_ascending
