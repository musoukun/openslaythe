## Displays enemys tab in the codex
extends BaseMenu

# list of acts and enemy buttons
@onready var codex_act_enemy_button_container: VBoxContainer = %CodexActEnemyButtonContainer

# visibility container for selected enemy intents
@onready var codex_enemy_intents: Control = %CodexEnemyIntents
# visibility toggle for enemy intents
@onready var codex_enemy_intents_toggle_button: Button = %CodexEnemyIntentsToggleButton
# list of enemy intents
@onready var codex_enemy_intents_container: VBoxContainer = %CodexEnemyIntentsContainer

@onready var codex_enemy_name_label: Label = %CodexEnemyNameLabel
@onready var codex_enemy_texture: TextureRect = %CodexEnemyTexture
@onready var codex_enemy_health_label: Label = %CodexEnemyHealthLabel

func _ready() -> void:
	codex_enemy_intents_toggle_button.toggled.connect(_on_enemy_intent_toggle)

func populate_menu() -> void:
	super()
	_populate_codex_enemies()

func clear_menu() -> void:
	super()
	clear_codex_enemies()
	clear_codex_enemy_intents()

func clear_codex_enemies() -> void:
	for child: Node in codex_act_enemy_button_container.get_children():
		child.queue_free()

func clear_codex_enemy_intents() -> void:
	for child: Node in codex_enemy_intents_container.get_children():
		child.queue_free()

func _populate_codex_enemies() -> void:
	clear_codex_enemies()
	
	# Get all acts, sort them, then list the enemies that appear in those acts, sorted into the acts
	var act_list: Array = Global._id_to_act_data.values()
	act_list.sort_custom(_codex_act_sort)
	var enemy_id_set: Dictionary[String, Variant] = {} # used as a set for ensuring no duplicate enemies are listed
	
	for act_data: ActData in act_list:
		# create label for the act
		var codex_act_name_label = Scenes.CODEX_ACT_NAME_LABEL.instantiate()
		codex_act_enemy_button_container.add_child(codex_act_name_label)
		codex_act_name_label.init(act_data)
		
		# get the enemies of the acts and sort by type/name
		var act_enemy_ids: Array[String] = act_data.get_act_all_enemy_ids()
		act_enemy_ids.sort_custom(_codex_enemy_sort)
		
		# generate buttons
		var first_enemy_button: bool = false
		for enemy_id: String in act_enemy_ids:
			if not enemy_id_set.has(enemy_id):
				# mark the enemy as seen
				enemy_id_set[enemy_id] = null
				# create button for the act
				var codex_enemy_button: CodexEnemyButton = Scenes.CODEX_ENEMY_BUTTON.instantiate()
				var enemy_data: EnemyData = Global.get_enemy_data(enemy_id)
				
				codex_act_enemy_button_container.add_child(codex_enemy_button)
				codex_enemy_button.init(enemy_data)
				
				codex_enemy_button.codex_enemy_button_up.connect(_on_codex_enemy_button_up)
				
				# simulate pressing the first enemy so it displays
				if not first_enemy_button:
					populate_codex_enemy(enemy_data)
					first_enemy_button = true

## Populates info for a given enemy, and a list of intents
func populate_codex_enemy(enemy_data: EnemyData) -> void:
	codex_enemy_name_label.text = enemy_data.enemy_name
	codex_enemy_texture.texture = FileLoader.load_texture(enemy_data.enemy_texture_path)
	codex_enemy_health_label.text = "HP: {0}-{1}".format([enemy_data.enemy_health_max_random_lower, enemy_data.enemy_health_max_random_upper])
	populate_codex_enemy_intents(enemy_data)

## Populates a list of intents for a given enemy
func populate_codex_enemy_intents(enemy_data: EnemyData) -> void:
	clear_codex_enemy_intents()
	
	for intent_id: String in enemy_data.enemy_intents:
		if intent_id == EnemyIntentData.INTENT_INITIAL:
			continue # ignore the initial intent
		var enemy_intent_data: EnemyIntentData = enemy_data.enemy_intents[intent_id]
		
		# skip non zero difficulty intents
		if enemy_intent_data.enemy_intent_difficulty_level > 0:
			continue # NOTE: You may wish to change this behavior
		
		# create intent
		var codex_enemy_intent: CodexEnemyIntent = Scenes.CODEX_ENEMY_INTENT.instantiate()
		codex_enemy_intents_container.add_child(codex_enemy_intent)
		codex_enemy_intent.init(enemy_data, enemy_intent_data)

func _on_codex_enemy_button_up(enemy_data: EnemyData) -> void:
	populate_codex_enemy(enemy_data)

func _on_enemy_intent_toggle(toggle: bool) -> void:
	codex_enemy_intents.visible = toggle

func _codex_act_sort(act_data_1: ActData, act_data_2: ActData) -> bool:
	if act_data_1.act_codex_number == act_data_2.act_codex_number:
		return act_data_1.act_name < act_data_2.act_name
	else:
		return act_data_1.act_codex_number < act_data_2.act_codex_number

# sorts the enemies by type (standard, miniboss, boss) and then name
func _codex_enemy_sort(enemy_id_1: String, enemy_id_2: String) -> bool:
	var enemy_data_1: EnemyData = Global.get_enemy_data(enemy_id_1)
	var enemy_data_2: EnemyData = Global.get_enemy_data(enemy_id_2)
	if enemy_data_1.enemy_type == enemy_data_2.enemy_type:
		return enemy_data_1.enemy_name < enemy_data_2.enemy_name
	else:
		return enemy_data_1.enemy_type < enemy_data_2.enemy_type
