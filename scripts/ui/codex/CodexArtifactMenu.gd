## Displays artifacts tab in the codex
extends BaseMenu

@onready var codex_artifact_rgsc: ResizingGridScrollContainer = %CodexArtifactRGSC

var sort_by_artifact_rarity: bool = true # if artifacts should be sorted by rarity
var subsort_by_artifact_name_ascending: bool = true

func populate_menu() -> void:
	super()
	_populate_codex_artifacts()
	
	codex_artifact_rgsc.call_deferred("resize_grid_columns")

func clear_menu() -> void:
	super()
	codex_artifact_rgsc.clear_children()

# creates display artifacts in codex
func _populate_codex_artifacts() -> void:
	var artifact_args: Array[Array] = [] # used to instantiate artifacts in container
	var artifact_object_ids: Array = Global._id_to_artifact_data.keys()

	# generate data to make artifacts
	for artifact_object_id: String in artifact_object_ids:
		var artifact_data: ArtifactData = Global.get_artifact_data(artifact_object_id)
		artifact_args.append([artifact_data])
	
	if len(artifact_args) > 1:
		artifact_args.sort_custom(_codex_artifact_custom_sort)
	
	# populate artifacts
	codex_artifact_rgsc.populate_children(Scenes.CODEX_ARTIFACT, artifact_args)

func _codex_artifact_custom_sort(artifact_args_1: Array, artifact_args_2: Array) -> bool:
	var artifact_data_1: ArtifactData = artifact_args_1[0]
	var artifact_data_2: ArtifactData = artifact_args_2[0]
	if sort_by_artifact_rarity:
		if artifact_data_1.artifact_rarity == artifact_data_2.artifact_rarity:
			return (artifact_data_1.artifact_name < artifact_data_2.artifact_name) == subsort_by_artifact_name_ascending
		return artifact_data_1.artifact_rarity < artifact_data_2.artifact_rarity
	else:
		return (artifact_data_1.artifact_name < artifact_data_2.artifact_name) == subsort_by_artifact_name_ascending
