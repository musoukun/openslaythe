## UI component representing a CardData.
## Used whenever a card needs to be visibly displayed.
extends Control
class_name Card

var card_data: CardData = null
## Maps a CardDececorator object_id to a CardDecorator subcomponent
var card_decorator_id_to_card_decorator: Dictionary[String, CardDecorator] = {}

const CARDS_RERENDER_LAZILY: bool = true # throttles card display generation to next frame
var _card_is_rerendering: bool = false

## The label text for energy will be displayed in this color if the player has enough energy
## to play the card.
const CARD_SUFFICIENT_ENERGY_LABEL_COLOR: Color = Color.WHITE
## The label text for energy will be displayed in this color if the player does not have enough energy
## to play the card.
const CARD_INSUFFICIENT_ENERGY_LABEL_COLOR: Color = Color.FIREBRICK
const CARD_TEXT_IMAGE_SIZE: int = 16	# images in card descriptions will be set to this size
const ENERGY_ICON_KEYWORD: String = "[energy_icon]"	# tells description to display an energy icon in place

var tooltip_left_side: bool = false # if tooltip should display to the left of the card when hovered

@onready var card_button: Button = %CardButton

@onready var pivot: Node2D = $Pivot # used to rotate/offset the card
@onready var card_visual: Control = $Pivot/CardVisual # container for all visuals in card, after pivot offsets applied

@onready var card_texture = %CardTexture
@onready var card_name: RichLabelAutoSizer = %CardName
@onready var card_type: Label = %CardType
@onready var card_description: RichLabelAutoSizer = %CardDescription
@onready var card_energy_sprite: TextureRect = %EnergySprite
@onready var card_energy_cost_label: Label = %EnergyCost
@onready var card_color: ColorRect = %ColorBackground
@onready var card_decorator_container: VBoxContainer = %CardDecoratorContainer

@onready var animation_player: AnimationPlayer = $AnimationPlayer
@onready var card_glow: ColorRect = %CardGlow

@onready var keyword_timer = $KeywordTimer

const KEYWORD_HOVER_DELAY: float = 0.5

signal card_selected(Card)
signal card_right_clicked(Card)
signal card_hovered(Card)
signal card_unhovered(Card)

func init(_card_data: CardData, angular_offset: float, connect_combat_signals: bool = false, connect_ui_signals: bool = true):
	card_data = _card_data
	pivot.rotation_degrees = angular_offset
	
	# signals used for cards in player's hand
	if connect_combat_signals:
		Signals.card_discarded.connect(_on_card_discarded)
		Signals.card_play_started.connect(_on_card_play_started)
		Signals.card_exhausted.connect(_on_card_exhausted)
		Signals.card_banished.connect(_on_card_banished)
		Signals.card_drawn.connect(_on_card_drawn)
		Signals.card_added_to_draw.connect(_on_card_added_to_draw)
		Signals.card_properties_changed.connect(_on_card_properties_changed)
		Signals.card_turn_energy_changed.connect(_on_card_turn_energy_changed)
		Signals.card_upgraded.connect(_on_card_upgraded)
		Signals.card_transformed.connect(_on_card_transformed)
		Signals.card_decorators_changed.connect(_on_card_decorators_changed)
	# flag to disable cards so they're not interactable by player
	if connect_ui_signals:
		card_button.gui_input.connect(_on_button_gui_input)
		card_button.mouse_entered.connect(_on_mouse_entered)
		card_button.mouse_exited.connect(_on_mouse_exited)
		keyword_timer.timeout.connect(_on_keyword_timeout)
	
	update_card_display()
	
	# initialize card decorators
	# NOTE: If the Card is not in hand BaseCardDecorator will not attach signal listeners by default,
	# only modify card visuals.
	card_decorator_id_to_card_decorator = _generate_card_decorators(card_data.card_decorators)
	

func update_card_display(selected_enemy: Enemy = null) -> void:
	if _card_is_rerendering:
		return
	if CARDS_RERENDER_LAZILY:
		_card_is_rerendering = true
		await get_tree().process_frame
		_card_is_rerendering = false
	
	# update visuals
	card_texture.texture = FileLoader.load_texture(card_data.card_texture_path)
	
	# updates the card's display
	card_name.set_bbcode("[center]" + card_data.get_card_name() + "[/center]")
	card_description.set_bbcode(get_card_description(selected_enemy))
	card_type.text = CardData.CARD_RARITIES.keys()[card_data.card_rarity] + " " + CardData.CARD_TYPES.keys()[card_data.card_type]
	
	# update energy
	_update_energy_display(selected_enemy)

## Specifically updates the energy cost display of the card. This is seperated from  because it
## can be messed with depending on interception and card play validation
func _update_energy_display(selected_enemy: Enemy = null) -> void:
	# flags used for determining playability
	var card_is_in_hand: bool = _is_card_in_hand()
	
	# set the energy texture
	var color_data: ColorData = Global.get_color_data(card_data.card_color_id)
	if color_data != null:
		card_color.color = color_data.color
		if color_data.color_energy_icon_texture_path != "":
			card_energy_sprite.texture = FileLoader.load_texture(color_data.color_energy_icon_texture_path)
	
	# reset energy cost display
	card_energy_sprite.visible = card_data.card_is_playable
	card_energy_cost_label.modulate = CARD_SUFFICIENT_ENERGY_LABEL_COLOR
	
	# manage energy cost using an interceptor and dummy card play
	var card_play_intercepted_action_results: Dictionary[String, Variant] = card_data.get_card_play_intercepted_action_results(selected_enemy)
	var card_energy_cost: int = card_play_intercepted_action_results.get("card_energy_cost", card_data.get_card_energy_cost(true, true))
	var card_energy_cost_variable_upper_bound: int = card_play_intercepted_action_results.get("card_energy_cost_variable_upper_bound", card_data.card_energy_cost_variable_upper_bound)

	var card_unplayable: bool = not can_play_card(selected_enemy, false)
	
	if card_data.card_energy_cost_is_variable:
		card_energy_cost_label.text = "X"
		if card_energy_cost_variable_upper_bound >= 1:
			card_energy_cost_label.text = "X-" + str(card_energy_cost_variable_upper_bound)
	else:
		card_energy_cost_label.text = str(card_energy_cost)
	
	if card_is_in_hand and card_unplayable:
		card_energy_cost_label.modulate = CARD_INSUFFICIENT_ENERGY_LABEL_COLOR

func set_card_glow(_visible: bool) -> void:
	card_glow.visible = _visible

func toggle_card_glow() -> void:
	card_glow.visible = !card_glow.visible

## Returns if a card can be played, factoring in intercepted energy costs, validators, and other
## factors. Must pass all checks to be considered playable.
## display_player_messages = true will display a speech bubble above player saying what failed.
func can_play_card(selected_enemy: Enemy = null, display_player_messages: bool = true) -> bool:
	# cards not in hand are considered playable. This includes cards in shops, piles, and drafts.
	if not _is_card_in_hand():
		return true
	
	# manage energy cost using an interceptor and dummy card play
	var card_play_intercepted_action_results: Dictionary[String, Variant] = card_data.get_card_play_intercepted_action_results(selected_enemy)
	var card_energy_cost: int = card_play_intercepted_action_results.get("card_energy_cost", card_data.get_card_energy_cost(true, true)) # variable cost is zero cost
	var card_is_playable = card_play_intercepted_action_results.get("card_is_playable", card_data.card_is_playable)
	var card_ignores_validators: bool = card_play_intercepted_action_results.get("card_ignores_validators", false)
	
	# check basic playability flag
	if not card_is_playable:
		return false
	
	# check energy
	if Global.player_data.player_energy < card_energy_cost:
		if display_player_messages:
			ActionGenerator.generate_insufficient_energy_speech_bubble()
		return false
	
	# check validators
	var card_passes_play_validators: bool = true
	if not card_ignores_validators:
		card_passes_play_validators = _validate_card()
	if not card_passes_play_validators:
		return false
	
	return true # card passes all checks and is playable

func get_card_description(selected_target: BaseCombatant = null) -> String:
	# generates a card description for a card
	var modified_description_bb_code: String = card_data.get_card_description()
	modified_description_bb_code = "[center]" + modified_description_bb_code + "[/center]"

	# generate fake card request
	var card_play_request: CardPlayRequest = HandManager.create_card_play_request(card_data, selected_target, true, true) # generate fake request
	
	# figure out what actions/values to calculate for the preview
	var card_description_preview_data: Array[Array] = []
	if len(card_data.card_description_preview_overrides) == 0:
		# with no overrides, assume basic block and attack
		card_description_preview_data = [
		 ["damage", Scripts.ACTION_ATTACK],
		 ["block", Scripts.ACTION_BLOCK]
		]
	else:
		# use the card's preview overrides
		card_description_preview_data = card_data.card_description_preview_overrides
	
	var player: Player = Global.get_player()
	
	# iterate over the preview data to determine any differences in the card's values
	for preview_data in card_description_preview_data:
		if len(preview_data) >= 2:
			var key_name: String = preview_data[0]
			var action_script_path: String = preview_data[1]
			
			if card_data.card_description.contains("[" + key_name + "]"):
				var action_data: Array[Dictionary] = [{action_script_path: {}}]
				var generated_action: BaseAction = ActionGenerator.create_actions(player, card_play_request, [selected_target], action_data, null)[0]
				var action_interceptor_processor: ActionInterceptorProcessor = generated_action._intercept_action([selected_target], true)[0]
				
				var card_value: int = card_data.card_values.get(key_name, 0)
				var value_substring: String = str(card_value)
				
				if action_interceptor_processor.shadowed_action_values.has(key_name):
					var intercepted_value: int = action_interceptor_processor.get_shadowed_action_values(key_name, card_value)
					
					# compare the intercepted valus to the card's values
					if intercepted_value < card_value:
						value_substring = "[color=red]" + str(intercepted_value) + "[/color]" # worse: red
					if intercepted_value > card_value:
						value_substring = "[color=green]" + str(intercepted_value) + "[/color]" # better: green
				
				modified_description_bb_code = modified_description_bb_code.replace("["+key_name+"]", value_substring)
			
	# do a second pass for non intercepted values in card description
	for key_name in card_data.card_values.keys():
		var non_intercepted_value: Variant = card_data.card_values[key_name]
		if non_intercepted_value is float:
			non_intercepted_value = int(non_intercepted_value)
		modified_description_bb_code = modified_description_bb_code.replace("["+key_name+"]", str(non_intercepted_value))
	
	# replace energy icon with external image bbcode
	if card_data.card_description.contains(ENERGY_ICON_KEYWORD):
		if card_data.card_color_id != "":
			var color_data: ColorData = Global.get_color_data(card_data.card_color_id)
			if color_data != null:
				if color_data.color_energy_icon_texture_path != "":
					var image_bb_code: String = "[img width={0}]{1}[/img]".format([CARD_TEXT_IMAGE_SIZE, color_data.color_energy_icon_texture_path])
					modified_description_bb_code = modified_description_bb_code.replace(ENERGY_ICON_KEYWORD, image_bb_code)
	
	return modified_description_bb_code

func _clear_card_decorators() -> void:
	for child: Control in card_decorator_container.get_children():
		child.queue_free()
	card_decorator_id_to_card_decorator.clear()

func _generate_card_decorators(card_decorators: Dictionary[String, Dictionary]) -> Dictionary[String, CardDecorator]:
	var generated_card_decorators: Dictionary[String, CardDecorator] = {}
	_clear_card_decorators()
	for card_decorator_id: String in card_decorators:
		var card_decorator_data: CardDecoratorData = Global.get_card_decorator_data(card_decorator_id)
		if card_decorator_data.card_decorator_script_path != "":
			var card_decorator: CardDecorator = Scenes.CARD_DECORATOR.instantiate()
			card_decorator_container.add_child(card_decorator)
			card_decorator.init(self, card_decorator_id)
			generated_card_decorators[card_decorator_id] = card_decorator
	
	return generated_card_decorators
	

## Checks if card passes all validators to play it
func _validate_card() -> bool:
	return Global.validate(card_data.card_play_validators, card_data, null)

func _glow_validation() -> bool:
	# determines glow logic
	if len(card_data.card_glow_validators) == 0:
		if len(card_data.card_play_validators) > 0:
			return _validate_card() # if no glow validators use play validators
		else:
			return false
	else:
		# use glow validators
		return Global.validate(card_data.card_glow_validators, card_data, null)

func _is_card_in_hand() -> bool:
	return Global.is_run and HandManager.player_hand.has(card_data)

func _attempt_hand_glow() -> void:
	# tests to see if cards in hand that require validation meet validation and glow
	if _is_card_in_hand():
		set_card_glow(_glow_validation())

func _on_button_gui_input(event: InputEvent):
	if event.is_action_released("left_click"):
		card_selected.emit(self)
	if event.is_action_released("right_click"):
		card_right_clicked.emit(self)

func _on_mouse_entered():
	keyword_timer.start(KEYWORD_HOVER_DELAY)
	card_hovered.emit(self)
	
func _on_mouse_exited():
	keyword_timer.stop()
	HandManager.tooltip.hide_tooltip()
	card_unhovered.emit(self)

func _on_keyword_timeout():
	HandManager.tooltip.display_card_keywords(self)

func _on_card_properties_changed(_card_data: CardData):
	if card_data == _card_data:
		update_card_display()

func _on_card_turn_energy_changed(_card_data: CardData):
	if card_data == _card_data:
		update_card_display()

func _on_card_upgraded(_card_data: CardData):
	if card_data == _card_data:
		update_card_display()

func _on_card_transformed(_card_data: CardData):
	if card_data == _card_data:
		# # reset card decorators to newly transformed card and rerender if in hand
		if _is_card_in_hand():
			card_decorator_id_to_card_decorator = _generate_card_decorators(card_data.card_decorators)
			update_card_display()

func _on_card_decorators_changed(_card_data: CardData):
	if card_data == _card_data:
		if _is_card_in_hand():
			card_decorator_id_to_card_decorator = _generate_card_decorators(card_data.card_decorators)
			update_card_display()

func _on_card_discarded(_card_data: CardData, _is_manual_discard: bool):
	if card_data == _card_data:
		HandManager.hand.create_card_trail_from_card(self, HandManager.DISCARD_PILE, true)
		queue_free()
	else:
		_attempt_hand_glow()

func _on_card_added_to_draw(_card_data: CardData):
	if card_data == _card_data:
		HandManager.hand.create_card_trail_from_card(self, HandManager.DRAW_PILE, true)
		queue_free()
	else:
		_attempt_hand_glow()

func _on_card_play_started(card_play_request: CardPlayRequest):
	if card_data == card_play_request.card_data:
		HandManager.hand.create_card_trail_from_card(self, card_play_request.card_destination_pile, true)
		queue_free()
	else:
		_attempt_hand_glow()

func _on_card_exhausted(_card_data: CardData):
	if card_data == _card_data:
		HandManager.hand.create_card_trail_from_card(self, HandManager.EXHAUST_PILE, true)
		queue_free()
	else:
		_attempt_hand_glow()

func _on_card_banished(_card_data: CardData, _in_limbo: bool):
	if card_data == _card_data:
		queue_free()
	else:
		_attempt_hand_glow()

func _on_card_drawn(_card_data: CardData):
	_attempt_hand_glow()
