## Settings menu for the game.
## Update this to add more settings to the game.
## Syncs with UserSettingsData through _save_settings() on leaving menu.
extends BaseMenu

# settings components
@onready var master_volume_slider: HSlider = %MasterVolumeSlider
@onready var effects_volume_slider: HSlider = %EffectsVolumeSlider
@onready var music_volume_slider: HSlider = %MusicVolumeSlider

@onready var mute_background_check_button: CheckButton = %MuteBackgroundCheckButton

func _ready():
	super()
	mute_background_check_button.toggled.connect(_on_mute_background_button_toggled)
	
	master_volume_slider.value_changed.connect(_on_volume_slider_changed)
	effects_volume_slider.value_changed.connect(_on_volume_slider_changed)
	music_volume_slider.value_changed.connect(_on_volume_slider_changed)
	#
	## wait a frame to ensure settings load on game start
	#await get_tree().process_frame
	#populate_menu()
	#visible = false

## Pull settings from UI and save them. Happens when leaving the menu.
func _save_settings() -> void:
	Global.user_settings_data.settings_audio_master_volume = master_volume_slider.value
	Global.user_settings_data.settings_audio_effects_volume = effects_volume_slider.value
	Global.user_settings_data.settings_audio_music_volume = music_volume_slider.value
	
	Global.user_settings_data.settings_audio_mute_on_window_lose_focus = mute_background_check_button.button_pressed
	
	FileLoader.save_user_settings()

func populate_menu() -> void:
	super()
	# volume sliders
	master_volume_slider.value = Global.user_settings_data.settings_audio_master_volume
	effects_volume_slider.value = Global.user_settings_data.settings_audio_effects_volume
	music_volume_slider.value = Global.user_settings_data.settings_audio_music_volume
	# mute in background
	mute_background_check_button.set_pressed_no_signal(Global.user_settings_data.settings_audio_mute_on_window_lose_focus)

## Save settings before going to the next menu
func _navigate_to_next_menu(next_menu: BaseMenu) -> void:
	_save_settings()
	super(next_menu)

func clear_menu() -> void:
	super()

#region Settings Signals
## Called whenever any slider changes. Recomputes volumes
func _on_volume_slider_changed(_val: float):
	var master_volume: float = master_volume_slider.value
	var modified_music_volume: float = music_volume_slider.value * master_volume
	var modified_effects: float = effects_volume_slider.value * master_volume
	SoundManager.set_music_volume(modified_music_volume)
	SoundManager.set_sound_volume(modified_effects)

func _on_mute_background_button_toggled(toggle: bool):
	Global.user_settings_data.settings_audio_mute_on_window_lose_focus = toggle

#endregion
