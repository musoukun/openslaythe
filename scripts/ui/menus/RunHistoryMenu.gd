extends BaseMenu

# will hide everything if no runs exist
@onready var runs_exist_container: Control = $RunsExistContainer
@onready var no_runs_label: Label = $NoRunsLabel

# selects between next and previous run histories
@onready var previous_run_button: Button = $RunsExistContainer/PreviousRunButton
@onready var run_history_character_icon: TextureRect = $RunsExistContainer/RunHistoryCharacterIcon
@onready var run_history_difficulty_label: Label = $RunsExistContainer/RunHistoryCharacterIcon/RunHistoryDifficultyLabel
@onready var next_run_button: Button = $RunsExistContainer/NextRunButton

@onready var run_history_character_name_label: Label = $RunsExistContainer/RunHistoryCharacterNameLabel
@onready var run_history_seed_label: Label = $RunsExistContainer/RunHistorySeedLabel
@onready var run_history_completion_date_label: Label = $RunsExistContainer/RunHistoryCompletionDateLabel
@onready var run_history_message_label: RichTextLabel = $RunsExistContainer/RunHistoryMessageLabel
@onready var run_history_health_label: Label = $RunsExistContainer/RunHistoryHealthLabel
@onready var run_history_money_label: Label = $RunsExistContainer/RunHistoryMoneyLabel
@onready var run_history_run_time_label: Label = $RunsExistContainer/RunHistoryRunTimeLabel
@onready var run_history_floor_label: Label = $RunsExistContainer/RunHistoryFloorLabel

@onready var run_history_consumable_container: HBoxContainer = %RunHistoryConsumableContainer
@onready var run_history_card_container: GridContainer = %RunHistoryCardContainer
@onready var run_history_artifact_container: GridContainer = %RunHistoryArtifactContainer

## Selected index of all player runs
## If negative (default) it will select the most current run if it exists. 
var current_run_history_index: int = -1

const VICTORY_MESSAGE_BBCODE: String = "[color=green]Victory[/color]"
const MISSING_EVENT_BBCODE: String = "[color=red]ERROR: MISSING EVENT[/color]"

func _ready() -> void:
	super()
	previous_run_button.button_up.connect(_on_previous_run_button_up)
	next_run_button.button_up.connect(_on_next_run_button_up)

func populate_menu() -> void:
	super()
	_populate_run_history() # populate latest run by default
	

func _populate_run_history(run_index: int = -1) -> void:
	_clear_run_history()
	var profile_data: ProfileData = Global.profile_data
	var profile_run_history: Array[RunStatsData] = profile_data.profile_run_history
	
	# hide everything if no existing run history
	if len(profile_run_history) == 0:
		return
	# show full page
	no_runs_label.visible = false
	runs_exist_container.visible = true
	
	if run_index < 0:
		# populate run stats of latest run by default
		current_run_history_index = len(profile_run_history) - 1
	else:
		current_run_history_index = clamp(run_index, 0, len(profile_run_history) - 1)
	
	# display forward and back button visibility
	previous_run_button.visible = current_run_history_index > 0
	next_run_button.visible = current_run_history_index < len(profile_run_history) - 1
	
	# display run summary stats
	var run_stats_data: RunStatsData = profile_run_history[current_run_history_index]
	
	var character_data: CharacterData = Global.get_character_data(run_stats_data.run_character_id)
	
	# portrait and name
	if character_data == null:
		run_history_character_name_label.text = "Invalid Character"
		run_history_character_icon.texture = FileLoader.MISSING_TEXTURE
	else:
		run_history_character_name_label.text = character_data.character_name
		run_history_character_icon.texture = FileLoader.load_texture(character_data.character_icon_texture_path)
	
	run_history_difficulty_label.text = str(run_stats_data.run_difficulty_level)
	run_history_seed_label.text = "Seed: {0}".format([run_stats_data.run_seed])
	run_history_health_label.text = "HP: {0}/{1}".format([run_stats_data.run_player_health, run_stats_data.run_player_health_max])
	run_history_money_label.text = "Money: {0}".format([run_stats_data.run_player_money])
	run_history_floor_label.text = "Floor: {0}".format([run_stats_data.run_floor])
	
	if run_stats_data.run_victory:
		# victory
		run_history_message_label.parse_bbcode(VICTORY_MESSAGE_BBCODE)
	else:
		# defeat event message
		var defeat_event_data: EventData = Global.get_event_data(run_stats_data.run_defeat_event_id)
		if defeat_event_data == null:
			run_history_message_label.parse_bbcode(MISSING_EVENT_BBCODE)
		else:
			run_history_message_label.parse_bbcode(defeat_event_data.event_death_message_bbcode)
	
	var completion_date_str: String = Time.get_date_string_from_unix_time(run_stats_data.run_completion_timestamp)
	run_history_completion_date_label.text = "Completed {0}".format([completion_date_str])
	
	var run_time_str = Time.get_time_string_from_unix_time(int(run_stats_data.run_completion_time))
	run_history_run_time_label.text = "Run Length: {0}".format([run_time_str])
	
	# consumables remaining end of run
	var run_consumable_ids: Array = run_stats_data.run_consumable_ids
	for consumable_id: String in run_consumable_ids:
		var consumable_data: ConsumableData = Global.get_consumable_data(consumable_id)
		var codex_consumable: CodexConsumable = Scenes.CODEX_CONSUMABLE.instantiate()
		run_history_consumable_container.add_child(codex_consumable)
		codex_consumable.init(consumable_data)
		
	# end of run cards
	# need to sort the cards into buckets before instantiating history cards
	var run_deck: Array[Array] = run_stats_data.run_deck
	var card_id_to_upgrade_to_count: Dictionary = {}
	for card_tuple: Array in run_deck:
		# get id and upgrade level of each card in deck
		var card_id: String = card_tuple[0]
		var card_upgrade_level: int = card_tuple[1]
		
		# check if others of same id + level exist or create entry
		var card_upgrade_slots: Dictionary = card_id_to_upgrade_to_count.get(card_id, {})
		var card_count: int = card_upgrade_slots.get(card_upgrade_level, 0)
		# increment and set entry
		card_count += 1
		card_upgrade_slots[card_upgrade_level] = card_count
		card_id_to_upgrade_to_count[card_id] = card_upgrade_slots
	
	# create history cards
	for card_id: String in card_id_to_upgrade_to_count:
		var card_upgrade_slots: Dictionary = card_id_to_upgrade_to_count[card_id]
		for card_upgrade_level: int in card_upgrade_slots:
			var card_count: int = card_upgrade_slots[card_upgrade_level]
			var run_history_card: RunHistoryCard = Scenes.RUN_HISTORY_CARD.instantiate()
			run_history_card_container.add_child(run_history_card)
			run_history_card.init(card_id, card_upgrade_level, card_count)
	
	# end of run artifacts
	var run_artifact_ids: Array = run_stats_data.run_artifact_ids
	for artifact_id: String in run_artifact_ids:
		var artifact_data: ArtifactData = Global.get_artifact_data(artifact_id)
		var codex_artifact: CodexArtifact = Scenes.CODEX_ARTIFACT.instantiate()
		run_history_artifact_container.add_child(codex_artifact)
		codex_artifact.init(artifact_data)

func clear_menu() -> void:
	super()
	_clear_run_history()
	no_runs_label.visible = true
	runs_exist_container.visible = false
	

func _clear_run_history() -> void:
	for child: Node in run_history_consumable_container.get_children():
		child.queue_free()
	for child: Node in run_history_card_container.get_children():
		child.queue_free()
	for child: Node in run_history_artifact_container.get_children():
		child.queue_free()

func _on_previous_run_button_up() -> void:
	_populate_run_history(current_run_history_index - 1)
func _on_next_run_button_up() -> void:
	_populate_run_history(current_run_history_index + 1)
