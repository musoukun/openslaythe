## Base class for a main page, such as the main menu, new run menu, codex menu etc.
## Works like a webpage, with navigation buttons to the next page mapped via @exports
## Also optionally supports intro/outro animations via Tweens.
extends Control
class_name BaseMenu

## Maps a navigation button to the menu you wish to go to
@export var navigation_button_to_menu: Dictionary[Button, BaseMenu] = {}

## If a sub menu is displayed, clicking the navigation button will hide the current sub menu
## and display the next one.
@export var navigation_button_to_sub_menu: Dictionary[Button, BaseMenu] = {}

## The default sub-menu to display when populating this one. This can be null if there's no sub menus
@export var default_sub_menu: BaseMenu
var current_sub_menu: BaseMenu = null

func _ready() -> void:
	_bind_navigation_buttons()

#region Override These For Each BaseMenu
## Populates menu with whatever. May also populate the default submenu.
## Override then call with super() to fill the menu up
func populate_menu() -> void:
	# reset menu state
	clear_menu()
	visible = true
	# display submenu if it exists
	if default_sub_menu != null:
		current_sub_menu = default_sub_menu
		current_sub_menu.populate_menu()
	# play the intro animation for this menu if it exists
	var intro_tween: Tween = _get_menu_intro_tween()
	if intro_tween != null:
		intro_tween.play()

## Resets the menu, usually by deleting instantiated children to free memory.
func clear_menu() -> void:
	visible = false

## Optional override
## If you want some kind of animation (eg fade-out) via Tweens you can define it here.
## The animation will play before moving to the next menu.
## NOTE: If null (default behavior) no animation will play.
func _get_menu_outro_tween(_next_menu: BaseMenu = null) -> Tween:
	return null
## Optional override
## If you want some kind of animation (eg fade-in) via Tweens you can define it here.
## The animation will play when the menu is populated
## NOTE: If null (default behavior) no animation will play.
func _get_menu_intro_tween(_previous_menu: BaseMenu = null) -> Tween:
	return null

## Optional override.
## If true the game will not wait for the outro of this menu to finish before playing the intro of
## the next menu
func are_outro_intro_simultaneous() -> bool:
	return false


#endregion
#region Keep
func _bind_navigation_buttons() -> void:
	# bind menu selection buttons to show each menu
	for button: Button in navigation_button_to_menu:
		if button == null:
			breakpoint # likely a broken @export
			continue
		var menu: BaseMenu = navigation_button_to_menu[button]
		button.button_up.connect(_navigate_to_next_menu.bind(menu))
	# bind sub menu selection buttons
	for button: Button in navigation_button_to_sub_menu:
		if button == null:
			breakpoint # likely a broken @export
			continue
		var menu: BaseMenu = navigation_button_to_sub_menu[button]
		button.button_up.connect(_navigate_to_next_submenu.bind(menu))
		
	

## Navigates to a new menu, clearing this one
func _navigate_to_next_menu(next_menu: BaseMenu) -> void:
	clear_menu()
	if current_sub_menu != null:
		current_sub_menu.clear_menu()
	
	# play outro animation if it exists
	# potentially wait for outro to finish before playing intro animation
	var outro_tween: Tween = _get_menu_outro_tween()
	if outro_tween != null:
		outro_tween.play()
		if not are_outro_intro_simultaneous():
			await outro_tween.finished
	
	next_menu.populate_menu()

## Clears the old submenu and displays the next one
func _navigate_to_next_submenu(next_menu: BaseMenu) -> void:
	if current_sub_menu != null:
		current_sub_menu.clear_menu()
		current_sub_menu.visible = false # ensure it's invisible
		current_sub_menu = next_menu
	next_menu.populate_menu()
	next_menu.visible = true # ensure it's visible

#endregion
