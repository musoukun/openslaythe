extends BaseMenu

@onready var profile_wins_label: Label = %ProfileWinsLabel
@onready var profile_losses_label: Label = %ProfileLossesLabel
@onready var profile_win_rate_label: Label = %ProfileWinRateLabel
@onready var profile_current_win_streak_label: Label = %ProfileCurrentWinStreakLabel
@onready var profile_highest_win_streak_label: Label = %ProfileHighestWinStreakLabel
@onready var profile_play_time_label: Label = %ProfilePlayTimeLabel
@onready var profile_current_loss_streak_label: Label = %ProfileCurrentLossStreakLabel
@onready var profile_highest_loss_streak_label: Label = %ProfileHighestLossStreakLabel
@onready var profile_fastest_run_time: Label = %ProfileFastestRunTime

@onready var stat_container: GridContainer = %StatContainer

func populate_menu() -> void:
	super()
	var profile_data: ProfileData = Global.profile_data
	# Aggregate stats
	
	# wins/losses
	profile_wins_label.text = "Wins: {0}".format([profile_data.profile_total_wins])
	profile_losses_label.text = "Losses: {0}".format([profile_data.profile_total_losses])
	
	# win rate
	var total_runs: int = profile_data.profile_total_wins + profile_data.profile_total_losses
	var win_rate: float = float(profile_data.profile_total_wins) / float(max(total_runs, 1))
	var win_rate_formatted: String = "%0.2f" % (win_rate * 100)
	profile_win_rate_label.text = "Win Rate: {0}%".format([win_rate_formatted])
	
	# win/loss streaks
	profile_current_win_streak_label.text = "Current Win Streak: {0}".format([profile_data.profile_current_win_streak]) 
	profile_highest_win_streak_label.text = "Highest Win Streak: {0}".format([profile_data.profile_highest_win_streak]) 
	profile_current_loss_streak_label.text = "Current Loss Streak: {0}".format([profile_data.profile_current_loss_streak]) 
	profile_highest_loss_streak_label.text = "Highest Loss Streak: {0}".format([profile_data.profile_highest_loss_streak]) 
	
	# format total run time
	var total_run_time_seconds: int = profile_data.profile_total_run_time
	var datetime_dict: Dictionary = Time.get_datetime_dict_from_unix_time(int(profile_data.profile_total_run_time))
	# HH:MM:SS
	profile_play_time_label.text = "Total Play Time: %02d:%02d:%02d" % [datetime_dict["hour"],
		datetime_dict["minute"],
		datetime_dict["second"],
	]
	
	# format fastest run time
	var fastest_run_time_seconds: int = int(profile_data.profile_fastest_win_run_time)
	datetime_dict = Time.get_datetime_dict_from_unix_time(fastest_run_time_seconds)
	# HH:MM:SS
	profile_fastest_run_time.text = "Fastest Win Time: %02d:%02d:%02d" % [datetime_dict["hour"],
		datetime_dict["minute"],
		datetime_dict["second"],
	]
	
	# character stats
	for character_id: String in Global._id_to_character_data:
		var character_stat: CharacterStat = Scenes.CHARACTER_STAT.instantiate()
		stat_container.add_child(character_stat)
		character_stat.init(character_id)

func clear_menu() -> void:
	super()
	for child: Node in stat_container.get_children():
		child.queue_free()
