## Globally accessible singleton allowing external access to Hand UI component and for manipulating cards
## such as playing them.
## Supports standard and custom card piles.
## See Hand for UI portion
## NOTE: Use ActionGenerator to create interceptable Hand related actions, which call these methods
extends Node

## How many cards player naturally draws at start of turn. See: ActionGenerator.generate_start_of_turn_draw_actions().
const PLAYER_CARD_DRAW_PER_TURN: int = 5

## The default max number of cards available in player's hand. Exceeding this will discard the cards.
## Intercept ActionAddCardsToHand and ActionDraw's hand_card_count_max to adjust the max hand size.
const PLAYER_DEFAULT_HAND_CARD_COUNT_MAX: int = 10

## If multipe cards are queueed how long it takes between them. Tweaking this will speed up plays slightly
const TIME_BETWEEN_CARD_PLAYS: float = 0.2

## If cards were exhausted end of turn, wait this amount
const EXHAUST_TIMER: float = 0.5

var hand: Hand = null # this will store a backreference to Hand UI on game load
var tooltip: Tooltip = null # this will store a backreference to Tooltip UI on game load. Not the best place to store it, but whatever

# Play
var card_play_queue: Array[CardPlayRequest] = []	# array of cards to play
var card_play_queue_reserved_energy_total: int = 0	# used to determine how much energy player is using
var cards_being_played: bool = false
var CARD_NO_ENERGY_COST: int = -1
## Temp retained cards. Does not included cards with retain.
var cards_retained_this_turn: Array[CardData] = []
## Keeps track of cards that have their per turn energy modified, to reset at end of turn
var cards_with_modified_turn_energy: Array[CardData] = []

# Standard combat related piles
var player_hand: Array[CardData] = []
var player_draw: Array[CardData] = []
var player_discard: Array[CardData] = []
var player_exhaust: Array[CardData] = []

# magic strings for the various pile names. Used for HandManager.get_pile(), card pick actions, and CardData destinations.
const HAND_PILE: String = "HAND"
const DRAW_PILE: String = "DRAW" 
const DISCARD_PILE: String = "DISCARD" # this does not trigger discard effects. Used for play/end of turn destination for pretty much every standard card.
const MANUAL_DISCARD_PILE: String = "MANUAL_DISCARD" # triggers discard effects. Still goes to same place as discard via STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME
const EXHAUST_PILE: String = "EXHAUST"
const BANISH_PILE: String = "" # cards are completely removed from play when assigned here
# magic strings for other "pile" selections in HandManager.get_pile()
const DECK: String = "DECK"
const COMBAT_DECK: String = "COMBAT_DECK"
const PLAYED_THIS_TURN: String = "PLAYED_THIS_TURN"
const PLAYED_LAST_TURN: String = "PLAYED_LAST_TURN"

## This is used for destinations when adding cards to a pile. Realistically this only factors
## in when dealing with the draw or discard pile, but a generic implementation allows for other piles
## to use similar logic.
enum PILE_INSERTION_STRATEGIES {
	TOP, # the end of the array via .push_back()
	BOTTOM, # front of the array via push_front()
	RANDOM # random insertion
	}

## Maps pile names to the corresponding pile variable name in HandManager.
## This simplifies a lot of annoying pile management logic
## by letting me .get() and .set() it.
const STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME: Dictionary[String, String] = {
	HAND_PILE: "player_hand",
	DRAW_PILE: "player_draw",
	DISCARD_PILE: "player_discard",
	MANUAL_DISCARD_PILE: "player_discard",
	EXHAUST_PILE: "player_exhaust",
}

## This allows a generic extensible implementation of piles (Array[CardData]), should you wish to add more than
## the standard ones. See add_custom_pile().
var custom_piles: Dictionary[String, Array] = {
}

## Helper var used to see if the deck ui picking should be used.
## If you add a pile modify this.
var DECK_PICK_TYPES: Array[String] = [	
	HandManager.COMBAT_DECK,
	HandManager.DECK,
	HandManager.DRAW_PILE,
	HandManager.DISCARD_PILE,
	HandManager.EXHAUST_PILE,
	HandManager.PLAYED_LAST_TURN,
	HandManager.PLAYED_THIS_TURN,
	]

## Maps a card destination to a given backreference of a UI element for where a CardTrail should go.
## See Combat
var card_destination_to_ui_elements: Dictionary[String, Control] = {
	# ex HandManager.DISCARD_PILE: $DiscardPile
}

func _ready() -> void:
	Signals.card_played.connect(_on_card_played)
	Signals.card_turn_energy_changed.connect(_on_card_turn_energy_changed)
	
	Signals.combat_started.connect(_on_combat_started)
	Signals.combat_ended.connect(_on_combat_ended)
	Signals.run_ended.connect(_on_run_ended)
	Signals.player_killed.connect(_on_player_killed)

#region Pile Management

## Iterates over each card in the player's deck, making a copy of it and assigning the parent to the copied card
## This is done at the start of combat.
func generate_combat_deck() -> Array[CardData]:
	var combat_deck: Array[CardData] = []
	for card_data in Global.player_data.player_deck:
		var copied_card = card_data.duplicate(true)
		copied_card.parent_card = card_data
		combat_deck.append(copied_card)
	return combat_deck

## Gets the player's deck ready for combat.
func reset_deck() -> void:
	var combat_deck: Array[CardData] = generate_combat_deck()
	player_draw = combat_deck	# copy deck into player's draw pile
	player_discard = []
	player_exhaust = []
	player_discard = []
	player_hand = []
	_empty_custom_piles()
	
	hand.clear_hand_cards()
	shuffle_draw(true, false)

## Removes a card from everything in player's deck and hand.
## Useful for resetting a card to move it into one place.
func move_card_to_limbo(card_data: CardData) -> void:
	if card_data == null:
		return
	player_draw.erase(card_data)
	player_discard.erase(card_data)
	player_exhaust.erase(card_data)
	player_hand.erase(card_data)
	# unregister from Hand UI
	hand.card_data_to_hand_card.erase(card_data)

## Generic method for moving a card to a given pile.
## This is used mainly for card plays and end of turn card actions.
## Supports custom piles.
## You may wish to use more specific methods for clarity.
func move_card_to_pile(card_data: CardData, card_pile_origin: String, card_destination_pile: String, card_destination_strategy: int, hand_size: int, perform_actions: bool = true, send_signal: bool = true) -> void:
	# ensure card is removed from all piles
	move_card_to_limbo(card_data)
	
	# banished cards go nowhere, so you can just stop here
	if card_destination_pile == BANISH_PILE:
		if send_signal:
			Signals.card_banished.emit(card_data, false)
		return
	
	# get the pile
	var card_pile: Array[CardData] = []
	if STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME.has(card_destination_pile):
		var pile_property_name: String = STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME[card_destination_pile]
		card_pile = get(pile_property_name) # standard piles can use .get() to directly pull property into array
	elif custom_piles.has(card_destination_pile):
		card_pile = custom_piles[card_destination_pile] # get from custom pile
	else:
		breakpoint
		DebugLogger.log_error("Invalid pile provided: {0}".format([card_destination_pile]))
		HandManager.move_card_to_pile(card_data, HAND_PILE, DISCARD_PILE, PILE_INSERTION_STRATEGIES.TOP, hand_size, perform_actions, send_signal)
		return
	
	# perform insertion strategy
	if card_destination_pile == HAND_PILE:
		# hand size exceeded, discard card instead
		if len(player_hand) >= hand_size:
			Signals.card_hand_limit_reached.emit()
			HandManager.move_card_to_pile(card_data, HAND_PILE, DISCARD_PILE, PILE_INSERTION_STRATEGIES.TOP, hand_size, perform_actions, send_signal)
			return
		
		# hand is a special case. You can only append to hand otherwise it looks weird
		card_pile.push_back(card_data)
	else:
		match card_destination_strategy:
			HandManager.PILE_INSERTION_STRATEGIES.BOTTOM:
				card_pile.push_front(card_data) # put card at bottom of pile
			HandManager.PILE_INSERTION_STRATEGIES.RANDOM:
				# randomly insert the card in the pile
				var rng_pile_insert: RandomNumberGenerator = Global.player_data.get_player_rng("rng_pile_insert")
				var card_insert_index: int = rng_pile_insert.randi_range(0, len(card_pile))
				card_pile.insert(card_insert_index, card_data)
			_, HandManager.PILE_INSERTION_STRATEGIES.TOP:
				card_pile.push_back(card_data) # put card at top of pile
	
	# set the pile to the new modified pile
	if STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME.has(card_destination_pile):
		set(card_destination_pile, card_pile)
	elif custom_piles.has(card_destination_pile):
		custom_piles[card_destination_pile] = card_pile
	
	# add card to hand if possible
	if card_destination_pile == HandManager.HAND_PILE:
		hand.create_cards_in_hand([card_data])
	
	# perform actions
	if perform_actions:
		if card_pile_origin != card_destination_pile:
			match card_destination_pile:
				HandManager.EXHAUST_PILE:
					_perform_card_actions(card_data, [null], card_data.card_exhaust_actions, false, false)
				HandManager.MANUAL_DISCARD_PILE:
					# manual discard into discard doesn't count
					if card_pile_origin != HandManager.DISCARD_PILE:
						_perform_card_actions(card_data, [null], card_data.card_discard_actions, false, false)
	
	# signal the card has been moved from one pile to another
	if send_signal:
		if card_pile_origin != card_destination_pile:
			match card_destination_pile:
				HandManager.HAND_PILE:
					# adding a card from any other pile is considered adding to hand, even draw
					Signals.card_added_to_hand.emit(card_data)
				HandManager.EXHAUST_PILE:
					Signals.card_exhausted.emit(card_data)
				HandManager.DISCARD_PILE:
					Signals.card_discarded.emit(card_data, false)
				HandManager.MANUAL_DISCARD_PILE:
					Signals.card_discarded.emit(card_data, true)
				HandManager.DRAW_PILE:
					Signals.card_added_to_draw.emit(card_data)

## Gets all cards in a given pile.
## This supports standard and custom piles, as well as derived ones.
func get_pile(pile_name: String) -> Array[CardData]:
	# standard piles
	if STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME.has(pile_name):
		var pile_property_name: String = STANDARD_PILE_NAME_TO_PILE_PROPERTY_NAME[pile_name]
		var card_pile: Array[CardData] = get(pile_property_name)
		card_pile = card_pile.duplicate() # avoids pass-by-reference issues if you mutate the array
		return card_pile
	# custom piles
	if custom_piles.has(pile_name):
		var custom_card_pile: Array[CardData] = custom_piles[pile_name]
		custom_card_pile = custom_card_pile.duplicate() # avoids pass-by-reference issues if you mutate the array
		return custom_card_pile
	
	# special "piles" that are derived rather than actual piles
	match pile_name:
		HandManager.DECK:
			return Global.player_data.player_deck
		HandManager.COMBAT_DECK:
			var combat_deck: Array[CardData] = []
			combat_deck += player_draw
			combat_deck += player_discard
			combat_deck += player_hand
			return combat_deck
		HandManager.PLAYED_THIS_TURN:
			return StatsHandler.current_combat_stats.get_card_data_played_this_turn(false)
		HandManager.PLAYED_LAST_TURN:
			return StatsHandler.current_combat_stats.get_card_data_played_last_turn(false)
	
	# this code should never hit
	DebugLogger.log_error("HandManager.get_pile(): Invalid pile given: {0}".format([pile_name]))
	breakpoint
	return []

## Gets which card pile the card exists in combat.
## Returns a pile name.
func get_card_pile_location_name(card_data: CardData) -> String:
	var player_data: PlayerData = Global.player_data
	
	if player_hand.has(card_data):
		return HandManager.HAND_PILE
	if player_draw.has(card_data):
		return HandManager.DRAW_PILE
	if player_discard.has(card_data):
		return HandManager.DISCARD_PILE
	if player_exhaust.has(card_data):
		return HandManager.EXHAUST_PILE

	for pile_name: String in custom_piles:
		var pile: Array[CardData] = custom_piles[pile_name]
		if pile.has(card_data):
			return pile_name
	
	# banished as it does not exist in any pile
	return BANISH_PILE

## Allows dynamically registering card piles during runtime.
## If the pile has a ui element, card trails will go to it.
func add_custom_pile(pile_name: String, pile_ui_element: Control = null) -> void:
	if not custom_piles.has(pile_name):
		custom_piles[pile_name] = [] as Array[CardData]
		card_destination_to_ui_elements[pile_name] = pile_ui_element
		DECK_PICK_TYPES.append(pile_name) # this will default to pile selection ui when picking cards

## Resets contents of all custom card piles.
func _empty_custom_piles() -> void:
	for pile_name in custom_piles:
		custom_piles[pile_name] = [] as Array[CardData]

#endregion
#region Playing Cards

## Queues up a card to be played and automatically begins playing cards if none in queue
## Card play will cost energy if desired with require_energy. Manually playing cards always requres energy, while other sources may not.
## Cards can be made to play next with front_of_queue.
func add_card_to_play_queue(card_play_request: CardPlayRequest, require_energy: bool = true, front_of_queue: bool = false) -> void:
	var card_data: CardData = card_play_request.card_data
	if card_data == null:
		breakpoint
		DebugLogger.log_error("HandManager.add_card_to_play_queue: No card supplied for card play")
		return
	
	# get intercepted card play results to determine energy costs
	var card_play_intercepted_action_results: Dictionary[String, Variant] = card_data.get_card_play_intercepted_action_results(card_play_request.selected_target)
	var card_energy_cost: int = card_play_intercepted_action_results.get("card_energy_cost", card_data.get_card_energy_cost(true, true)) # variable cost is zero cost
	var card_energy_cost_variable_upper_bound: int = card_play_intercepted_action_results.get("card_energy_cost_variable_upper_bound", card_data.card_energy_cost_variable_upper_bound)
	
	# insufficient energy, don't add to queue
	if require_energy:
		if card_energy_cost > Global.player_data.player_energy:
			DebugLogger.log_error("HandManager.add_card_to_play_queue(): Insufficient energy")
			return
	
	card_play_queue.append(card_play_request)
	
	if require_energy and not card_play_request.is_duplicate_play:
		# reserve energy
		var energy_cost: int = card_energy_cost
		
		# variable cost cards consume all energy
		if card_data.card_energy_cost_is_variable:
			energy_cost = Global.player_data.player_energy
			# variable cost cards can have an upper bound to energy input
			if card_energy_cost_variable_upper_bound >= 0:
				energy_cost = min(energy_cost, card_energy_cost_variable_upper_bound)
		
		card_play_request.refundable_energy = energy_cost
		card_play_request.input_energy = energy_cost
		card_play_queue_reserved_energy_total += energy_cost
		
		# energy cost
		Global.player_data.player_energy -= energy_cost
		hand.combat.update_combat_display()
	else:
		# card doesn't require energy, reserve -1 energy in energy queue
		card_play_request.refundable_energy = CARD_NO_ENERGY_COST
		
		if card_data.card_energy_cost_is_variable:
			if card_play_request.is_duplicate_play:
				card_play_request.input_energy = Global.player_data.player_energy
		else:
			card_play_request.input_energy = 0
	
	# flip the card play to the front of the queue if desired
	if front_of_queue:
		card_play_queue.push_front(card_play_queue.pop_back())
	
	# automatically play the card if the current queue and actions performed are empty
	if (not ActionHandler.actions_being_performed) and (not cards_being_played):
		_perform_card_plays()

## Main looping/locking method for performing all cards in the card queue.
## WARNING: Do not invoke this anywhere other than add_card_to_play_queue()
func _perform_card_plays() -> void:	
	cards_being_played = true # lock the queue
	while len(card_play_queue) > 0:
		# no more enemies
		if not Global.are_remaining_enemies():
			clear_card_queue()
			break
		
		# pop the next card from the queue and play it
		var card_play_request: CardPlayRequest = card_play_queue.pop_front()
		var card_data: CardData = card_play_request.card_data
		var card_energy_cost = card_play_request.refundable_energy
		var card_target: BaseCombatant = card_play_request.selected_target
		card_play_queue_reserved_energy_total -= max(card_energy_cost, 0)
		
		# exit card queue if target no longer exists
		if card_target != null and not card_target.is_alive():
			card_play_queue.push_front(card_play_request)
			refund_card_queue()
			break
		
		_play_card(card_play_request)
		if ActionHandler.actions_being_performed:
			await ActionHandler.actions_ended
		
		await get_tree().create_timer(TIME_BETWEEN_CARD_PLAYS).timeout
	
	cards_being_played = false

## Actually plays the card: triggering its play actions, performing special play interception,
## and moving it to its destination pile, along with any corresponding signals.
## WARNING: Do not invoke this anywhere other than indirectly through add_card_to_play_queue().
func _play_card(card_play_request: CardPlayRequest) -> void:
	if card_play_request == null:
		DebugLogger.log_error("No CardPlayRequest during card play")
		breakpoint
		return
	if card_play_request.card_data == null:
		breakpoint
		DebugLogger.log_error("No CardData during card play")
		return	
	
	# store the state of the hand at play time in the play request
	card_play_request.hand_at_play_time = HandManager.player_hand.duplicate(false)
	
	# generate dummy card play action and intercept it
	# this allows some interceptors to access a card play as it's happening
	var generated_action: BaseAction = ActionGenerator.generate_card_play(card_play_request)
	var _action_interceptor_processors: Array[ActionInterceptorProcessor] = generated_action._intercept_action([null])
	
	# find out where the card came from before it's played
	var card_origin_pile: String = get_card_pile_location_name(card_play_request.card_data)
	card_play_request.card_origin_pile = card_origin_pile # actions can now know where the card was played from
	
	# remove card from hand and all piles for the duration of play
	# this prevents weird stuff from happening
	HandManager.move_card_to_limbo(card_play_request.card_data)
	
	# notify that the card play is beginning
	# NOTE: If you want certain things to happen right before a card play at this moment (like a status effect),
	# you can listen for the card_play_started and then add to the current action queue. Adding to the stack
	# however will not make the actions happen immediately before the action takes place and ordering will
	# not always be consistent.
	Signals.card_play_started.emit(card_play_request)
	# block in case card play starting triggers any effects
	if ActionHandler.actions_being_performed:
		await ActionHandler.actions_ended
	
	# get targets
	var targets: Array[BaseCombatant] = [card_play_request.selected_target]
	
	### generate and perform play card actions
	# generate the card actions
	var card_play_actions: Array[BaseAction] = ActionGenerator.create_actions(hand.player, card_play_request, targets, card_play_request.card_data.card_play_actions, null)
	# generate a special action which signifies the end of the card play, to be done after all the other card actions
	var card_played_finished_action: Array[BaseAction] = [ActionGenerator.generate_card_play_finished(card_play_request)]
	
	ActionHandler.add_actions(card_played_finished_action + card_play_actions)
	
	# update the hand while the actions are processing
	hand.tween_hand()
	hand.update_hand_card_display()
	
	# block until the card and any subsequent effects are completely finished
	if ActionHandler.actions_being_performed:
		await ActionHandler.actions_ended
	
	# move the card to its destination pile
	var card_destination_pile: String = card_play_request.card_destination_pile
	var card_destination_strategy: int = card_play_request.card_destination_strategy
	var is_duplicate_play: bool = card_play_request.is_duplicate_play
	var send_signal: bool = not is_duplicate_play
	HandManager.move_card_to_pile(card_play_request.card_data, card_origin_pile, card_destination_pile, card_destination_strategy, PLAYER_DEFAULT_HAND_CARD_COUNT_MAX, true, send_signal)
	
	hand.combat.update_combat_display()

## Discard non-retained cards, exhaust ethereal cards, and perform specific end of turn card actions
## NOTE: Called from Combat.
func perform_end_of_turn_hand_actions() -> void:
	# only the cards in hand at the end of turn matter for the purposes of performing actions on them
	# this prevents things like drawing a card end of turn then exhausting it because it's ethereal or whatever
	var duplicated_player_hand: Array[CardData] = HandManager.player_hand.duplicate(false)
	
	# reset cards with modified turn energy
	for card in HandManager.cards_with_modified_turn_energy:
		if card.card_energy_cost_until_turn > -1:
			card.set_card_energy_cost_until_turn(-1)
	HandManager.cards_with_modified_turn_energy.clear()
	
	# get cards with end of turn actions
	var end_of_turn_cards: Array[CardData] = []
	for card_data: CardData in duplicated_player_hand:
		if len(card_data.card_end_of_turn_actions) > 0:
			end_of_turn_cards.append(card_data)
	
	# perform the end of turn actions
	if len(end_of_turn_cards) > 0:
		for card_data: CardData in end_of_turn_cards:
			_perform_card_actions(card_data, [null], card_data.card_end_of_turn_actions, false, false)
		await Global.get_tree().create_timer(hand.CARD_TWEEN_TIME).timeout
	
	# wait for actions
	if ActionHandler.actions_being_performed:
		await ActionHandler.actions_ended
	
	# get all ethereal cards
	duplicated_player_hand = HandManager.player_hand.duplicate(false)
	var ethereal_cards: Array[CardData] = []
	for card_data in duplicated_player_hand:
		if card_data.is_card_ethereal():
			ethereal_cards.append(card_data)
	# exhaust them
	if len(ethereal_cards) > 0:
		exhaust_cards(ethereal_cards)
		# wait for tweens to finish
		await Global.get_tree().create_timer(EXHAUST_TIMER).timeout
	
	# wait for actions
	if ActionHandler.actions_being_performed:
		await ActionHandler.actions_ended
	
	# figure out if cards should be retained or moved to a different pile
	duplicated_player_hand = HandManager.player_hand.duplicate(false)
	var non_retained_cards: Array[CardData] = []
	for card_data: CardData in duplicated_player_hand:
		# check if card retained either temporarily or as a card flag/destination
		var card_is_retained: bool = card_data.does_card_retain() or HandManager.cards_retained_this_turn.has(card_data)
		# retained, perform retain actions
		if card_is_retained:
			_perform_card_actions(card_data, [null], card_data.card_retain_actions, false, false)
			Signals.card_retained.emit(card_data)
		else:
			non_retained_cards.append(card_data)
	
	# reset temp retained cards
	HandManager.cards_retained_this_turn.clear()
	#
	## wait for actions
	if ActionHandler.actions_being_performed:
		await ActionHandler.actions_ended
	
	# move non-retained cards to their respective end-of-turn piles
	for card_data: CardData in non_retained_cards:
		if duplicated_player_hand.has(card_data):
			var card_end_of_turn_destination: String = card_data.card_end_of_turn_destination
			var card_end_of_turn_destination_strategy: int = card_data.card_end_of_turn_destination_strategy
			move_card_to_pile(card_data, HAND_PILE, card_end_of_turn_destination, card_end_of_turn_destination_strategy, PLAYER_DEFAULT_HAND_CARD_COUNT_MAX, true, true)
			
			## wait for tween/card actions to finish
			#await Global.get_tree().create_timer(hand.CARD_TWEEN_TIME).timeout
	
	# wait for actions
	if ActionHandler.actions_being_performed:
		await ActionHandler.actions_ended

## Clears out the card queue.
func clear_card_queue() -> void:
	card_play_queue.clear()
	card_play_queue_reserved_energy_total = 0
	cards_being_played = false

## Clears out the card queue, refunding all the energy in it.
func refund_card_queue():
	for card_play_request in card_play_queue:
		Global.player_data.player_energy += card_play_request.refundable_energy
	clear_card_queue()
	Signals.card_queue_refunded.emit()

#endregion

#region Signals
func _on_combat_started(_event_id: String):
	hand._unprompt_target()
	HandManager.cards_retained_this_turn.clear()
	HandManager.cards_with_modified_turn_energy.clear()
	HandManager.clear_card_queue()
	HandManager.reset_deck()
	
func _on_combat_ended():
	HandManager.cards_retained_this_turn.clear()
	HandManager.cards_with_modified_turn_energy.clear()
	HandManager.clear_card_queue()
	
	hand._unprompt_target()
	
	# remove cards in hand
	hand.clear_hand_cards()

func _on_run_ended():
	HandManager.cards_retained_this_turn.clear()
	HandManager.cards_with_modified_turn_energy.clear()
	HandManager.clear_card_queue()
	
	# remove cards in hand
	hand.clear_hand_cards()
	reset_deck()

func _on_player_killed(_player: Player):
	discard_hand(false)

func _on_card_played(card_play_request: CardPlayRequest):
	# reset card play cost
	if card_play_request.card_data.card_energy_cost_until_played > -1:
		card_play_request.card_data.set_card_energy_cost_until_played(-1)

func _on_card_turn_energy_changed(card_data: CardData):
	# track cards with turn energy shadowing
	if card_data.card_energy_cost_until_turn > -1:
		if not HandManager.cards_with_modified_turn_energy.has(card_data):
			HandManager.cards_with_modified_turn_energy.append(card_data)

#endregion

#region Convenient Pile Management
## Draws a given number of cards and performs their effects.
## Can take a hand_card_count_max which restricts cards beyond the hand size limit.
## NOTE: This will almost always be called multiple times with card_number = 1 since each
## draw will be seperated on the stack as its own action.
func draw_cards(number_of_cards: int, hand_card_count_max: int) -> void:
	for i in number_of_cards:
		# hand full, stop drawing and move drawn card to discard
		if len(HandManager.player_hand) >= hand_card_count_max:
			Signals.card_hand_limit_reached.emit()
			break
		# check if enough cards to draw
		if len(HandManager.player_draw) == 0:
			# try to reshuffle
			if len(HandManager.player_discard) == 0:
				# no cards to shuffle into draw
				break
			else:
				shuffle_draw(true, true)
		
		# draw
		var card_data: CardData = HandManager.player_draw.pop_back()
		HandManager.player_hand.append(card_data)
		
		var _card: Card = hand.create_cards_in_hand([card_data])[0]
		# perform draw actions
		if len(card_data.card_draw_actions) > 0:
			_perform_card_actions(card_data, [null], card_data.card_draw_actions, false, false)
		
		Signals.card_drawn.emit(card_data)
		
	# rerender hand
	hand.tween_hand()

## Adds cards to draw pile.
func add_cards_to_draw(cards: Array[CardData], card_destination_strategy: int = PILE_INSERTION_STRATEGIES.TOP) -> void:
	for card_data: CardData in cards:
		var card_origin_pile: String = get_card_pile_location_name(card_data)
		move_card_to_pile(card_data, card_origin_pile, HandManager.DRAW_PILE, card_destination_strategy, PLAYER_DEFAULT_HAND_CARD_COUNT_MAX, true, true)
	
	hand.tween_hand()
	hand.update_hand_card_display()

## Adds cards directly to hand, discarding any additional ones if it's too full.
## Does not count as a draw.
## Can take a hand_card_count_max which restricts cards beyond the hand size limit.
func add_cards_to_hand(cards: Array[CardData], hand_card_count_max: int = HandManager.PLAYER_DEFAULT_HAND_CARD_COUNT_MAX) -> void:
	var discarded_cards: Array[CardData] = []
	var added_cards: Array[CardData] = []
	for card_data: CardData in cards:
		if not player_hand.has(card_data):
			if len(HandManager.player_hand) < hand_card_count_max:
				HandManager.move_card_to_limbo(card_data)
				var card: Card = hand.create_cards_in_hand([card_data])[0]
				HandManager.player_hand.append(card_data)

				Signals.card_added_to_hand.emit(card_data)
			else:
				discarded_cards.append(card_data)
	
	if len(discarded_cards) > 0:
		Signals.card_hand_limit_reached.emit()
		discard_cards(discarded_cards, false)
	
	hand.tween_hand()

func discard_hand(is_manual_discard: bool = false) -> void:
	discard_cards(player_hand, is_manual_discard)

## Sends card to the discard pile.
## If is_manual_discard is false, it is considered a "natural" discard and
## will not trigger card discard effects.
func discard_cards(cards: Array[CardData], is_manual_discard: bool = false) -> void:
	for card_data: CardData in cards:
		# transfer card
		HandManager.move_card_to_limbo(card_data)
		HandManager.player_discard.append(card_data)
		
		# perform discard actions
		if is_manual_discard:
			_perform_card_actions(card_data, [null], card_data.card_discard_actions, false, false)
		
		Signals.card_discarded.emit(card_data, is_manual_discard)
	
	# rerender hand
	hand.tween_hand()

func exhaust_cards(cards: Array[CardData]) -> void:
	for card_data: CardData in cards:
		move_card_to_limbo(card_data)
		HandManager.player_exhaust.append(card_data)
		
		_perform_card_actions(card_data, [null], card_data.card_exhaust_actions, false, false)
		
		Signals.card_exhausted.emit(card_data)
	
	# rerender hand
	hand.tween_hand()

func banish_cards(cards: Array[CardData], in_limbo: bool) -> void:
	for card_data: CardData in cards:
		move_card_to_limbo(card_data)
		Signals.card_banished.emit(card_data, in_limbo)
	
	# rerender hand
	hand.tween_hand()

## Temporarily adds cards to be retained for this turn in hand regardless of CardData retain.
func retain_cards_this_turn(cards: Array[CardData]) -> void:
	for card in cards:
		if not cards_retained_this_turn.has(card):
			cards_retained_this_turn.append(card)


## If you want to shuffle your discard into the draw, or simply randomize draw
## is_reshuffle counts for subsequent deck shuffles after the first.
func shuffle_draw(shuffle_discard_into_draw: bool = true, is_reshuffle: bool = true) -> void:
	if shuffle_discard_into_draw:
		HandManager.player_draw += HandManager.player_discard
		HandManager.player_discard = []
	
	var shuffle_rng: RandomNumberGenerator = Global.player_data.get_player_rng("rng_shuffle")
	
	# put cards into priority buckets, shuffle the individual buckets, then merge them
	var shuffled_draw: Array[CardData] = []
	var shuffle_priority_to_cards: Dictionary = {}
	# create buckets
	for card_data in HandManager.player_draw:
		var priority: int = card_data.card_first_shuffle_priority
		if is_reshuffle:
			priority = card_data.card_reshuffle_priority
		var card_bucket: Array[CardData] = []
		if shuffle_priority_to_cards.has(priority):
			card_bucket = shuffle_priority_to_cards[priority]
		card_bucket.append(card_data)
		shuffle_priority_to_cards[priority] = card_bucket
	# get sorted buckets
	var card_priorities: Array = shuffle_priority_to_cards.keys().duplicate()
	card_priorities.sort()
	# shuffle buckets and add them to draw
	for priority in card_priorities:
		var bucket_cards: Array[CardData] = shuffle_priority_to_cards[priority]
		bucket_cards = Random.get_weighted_card_shuffle(shuffle_rng, bucket_cards)
		shuffled_draw += bucket_cards
	# overwrite draw pile with bucket shuffled cards
	HandManager.player_draw = shuffled_draw
	
	# deck shuffle on first turn not counted as a deck reshuffle event
	Signals.card_deck_shuffled.emit(is_reshuffle)

## Disables the hand, preventing additional card plays by the player.
## Automatic plays can still happen.
func set_disable_hand(disabled: bool = true):
	hand.disable_hand(disabled)

## Simple factory method to cut down on code duplication
## WARNING: The pile destinations and strategies are not assigned here
func create_card_play_request(card_data: CardData, target: BaseCombatant, copy_card_values: bool = true, copy_hand: bool = true) -> CardPlayRequest:
	var card_play_request: CardPlayRequest = CardPlayRequest.new()
	card_play_request.card_data = card_data
	card_play_request.selected_target = target
	if copy_card_values:
		if card_data == null:
			DebugLogger.log_error("create_card_play_request(): No CardData provided to duplicate card_values")
		else:
			card_play_request.card_values = card_data.card_values.duplicate(true)
	if copy_hand:
		card_play_request.hand_at_play_time = HandManager.player_hand.duplicate(false)
	
	return card_play_request

## Small helper method to cut down on bloat. Simply creates a list of actions for a given card and adds it to the stack.
## These actions can be play/discard/exhaust/etc.
func _perform_card_actions(card_data: CardData, targets: Array[BaseCombatant], card_actions: Array[Dictionary], enqueue: bool = false, front_of_queue: bool = false) -> void:
	var player: Player = Global.get_player()
	# generate fake card play request
	var card_play_request: CardPlayRequest = HandManager.create_card_play_request(card_data, null, true, true)
	# perform actions
	var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(player, card_play_request, targets, card_actions, null)
	ActionHandler.add_actions(generated_actions, enqueue, front_of_queue)
